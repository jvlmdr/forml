import Mathlib.Analysis.Calculus.Deriv.Pi
import Mathlib.Analysis.Calculus.FDeriv.Pi
import Mathlib.Analysis.Distribution.SchwartzSpace

import ForML.ContinuousLinearMapCo
import ForML.ContinuousMultilinearMap
import ForML.MultilinearDeriv
import ForML.PiEquiv

open BigOperators

attribute [-simp] Matrix.zero_empty


-- Next define `pideriv` and use pi/copi to give some useful lemmata.

section Derivative

variable {Œπ ùïú : Type*}
variable [Fintype Œπ] [DecidableEq Œπ]
variable [NontriviallyNormedField ùïú]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
variable {M : Œπ ‚Üí Type*} [‚àÄ i, NormedAddCommGroup (M i)] [‚àÄ i, NormedSpace ùïú (M i)]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]

section Def
variable (ùïú)

/--
The Fr√©chet derivative with respect to one index of a pi type.
Supports `ùïú`-linearity for maps on `(‚àÄ i,) M i` with `NormedSpace ùïú (M i)`.
-/
noncomputable def fpideriv (i : Œπ) (f : (‚àÄ i, M i) ‚Üí F) (x : (‚àÄ i, M i)) : (M i) ‚ÜíL[ùïú] F :=
  fderiv ùïú (fun y => f (Function.update x i y)) (x i)

end Def

/-- The derivative with respect to one index of a pi type. -/
noncomputable def pideriv (i : Œπ) (f : (Œπ ‚Üí ùïú) ‚Üí F) (x : Œπ ‚Üí ùïú) : F :=
  deriv (fun y => f (Function.update x i y)) (x i)

lemma fpideriv_apply {i : Œπ} {f : (‚àÄ i, M i) ‚Üí F} {x : (‚àÄ i, M i)} {m : M i} :
    fpideriv ùïú i f x m = fderiv ùïú (fun y => f (Function.update x i y)) (x i) m := rfl

lemma fpideriv_def {i : Œπ} {f : (‚àÄ i, M i) ‚Üí F} :
    fpideriv ùïú i f = fun x => fderiv ùïú (fun y => f (Function.update x i y)) (x i) := rfl

lemma pideriv_def {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú} :
    pideriv i f x = deriv (fun y => f (Function.update x i y)) (x i) := rfl


/-- The function `f` has Fr√©chet derivative `f'` at `x` along  -/
def HasFPiDerivAt (i : Œπ) (f : (‚àÄ i, M i) ‚Üí F) (f' : M i ‚ÜíL[ùïú] F) (x : (‚àÄ i, M i)) :=
  HasFDerivAt (fun y => f (Function.update x i y)) f' (x i)

def HasPiDerivAt (i : Œπ) (f : (Œπ ‚Üí ùïú) ‚Üí F) (f' : F) (x : Œπ ‚Üí ùïú) :=
  HasDerivAt (fun y => f (Function.update x i y)) f' (x i)

theorem hasFPiDerivAt_iff_hasPiDerivAt {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {f' : ùïú ‚ÜíL[ùïú] F} {x : Œπ ‚Üí ùïú} :
    HasFPiDerivAt i f f' x ‚Üî HasPiDerivAt i f (f' 1) x :=
  hasFDerivAt_iff_hasDerivAt

lemma HasFDerivAt.hasFPiDerivAt {i : Œπ} {f : (‚àÄ i, M i) ‚Üí F} {f' : (‚àÄ i, M i) ‚ÜíL[ùïú] F} {x : (‚àÄ i, M i)}
    (hf : HasFDerivAt f f' x) :
    HasFPiDerivAt i f (f'.comp (ContinuousLinearMap.single i)) x := by
  rw [HasFPiDerivAt]
  refine HasFDerivAt.comp _ (by simpa) ?_
  refine HasFDerivAt.congr_fderiv (hasFDerivAt_update _ _) ?_
  ext m j
  simp
  simp [Pi.single]
  by_cases h : j = i
  . rw [h]
    simp
  . simp [h]

lemma HasFDerivAt.hasPiDerivAt {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {f' : (Œπ ‚Üí ùïú) ‚ÜíL[ùïú] F} {x : Œπ ‚Üí ùïú}
    (hf : HasFDerivAt f f' x) :
    HasPiDerivAt i f (f' (Pi.single i 1)) x := by
  change HasPiDerivAt i f (f'.comp (ContinuousLinearMap.single i) 1) x
  rw [‚Üê hasFPiDerivAt_iff_hasPiDerivAt]
  exact hasFPiDerivAt hf

lemma DifferentiableAt.hasFPiDerivAt {i : Œπ} {f : (‚àÄ i, M i) ‚Üí F} {x : (‚àÄ i, M i)}
    (hf : DifferentiableAt ùïú f x) :
    HasFPiDerivAt i f ((fderiv ùïú f x).comp (ContinuousLinearMap.single i)) x :=
  hf.hasFDerivAt.hasFPiDerivAt

lemma DifferentiableAt.hasPiDerivAt {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú}
    (hf : DifferentiableAt ùïú f x) :
    HasPiDerivAt i f ((fderiv ùïú f x) (Pi.single i 1)) x :=
  hf.hasFDerivAt.hasPiDerivAt

lemma HasFPiDerivAt.fpideriv {i : Œπ} {f : (‚àÄ i, M i) ‚Üí F} {f' : (M i) ‚ÜíL[ùïú] F} {x : (‚àÄ i, M i)} (hf : HasFPiDerivAt i f f' x) :
    fpideriv ùïú i f x = f' :=
  hf.fderiv

lemma HasPiDerivAt.pideriv {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {f' : F} {x : Œπ ‚Üí ùïú} (hf : HasPiDerivAt i f f' x) :
    pideriv i f x = f' :=
  hf.deriv

-- The partial derivative can be expressed as a projection of `fderiv` onto a canonical basis vector.

lemma fpideriv_eq_fderiv_comp_single {i : Œπ} {f : (‚àÄ i, M i) ‚Üí F} {x : (‚àÄ i, M i)}
    (hf : DifferentiableAt ùïú f x) :
    fpideriv ùïú i f x = (fderiv ùïú f x).comp (ContinuousLinearMap.single i) :=
  hf.hasFPiDerivAt.fpideriv

lemma pideriv_eq_fderiv_apply_single {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú}
    (hf : DifferentiableAt ùïú f x) :
    pideriv i f x = fderiv ùïú f x (Pi.single i 1) :=
  hf.hasPiDerivAt.pideriv

lemma fpideriv_apply_eq_fderiv_apply_single {i : Œπ} {f : (‚àÄ i, M i) ‚Üí F} {x : (‚àÄ i, M i)} {m : M i}
    (hf : DifferentiableAt ùïú f x) :
    fpideriv ùïú i f x m = fderiv ùïú f x (Pi.single i m) := by
  rw [fpideriv_eq_fderiv_comp_single hf]
  rfl

/-- Analogy of `fderiv_deriv`. -/
lemma fpideriv_pideriv {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú} :
    fpideriv ùïú i f x 1 = pideriv i f x :=
  fderiv_deriv

/-- Analogy of `deriv_fderiv`. -/
lemma pideriv_fpideriv {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú} :
    ContinuousLinearMap.smulRight (1 : ùïú ‚ÜíL[ùïú] ùïú) (pideriv i f x) = fpideriv ùïú i f x :=
  deriv_fderiv

lemma fpideriv_apply_eq_smul_pideriv {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú} {m : ùïú} :
    fpideriv ùïú i f x m = m ‚Ä¢ pideriv i f x := by
  rw [‚Üê pideriv_fpideriv]
  simp

/--
The Fr√©chet derivative can be rewritten as a `ContinuousLinearMap.copi`.

Compared to `fderiv_eq_copi_fpideriv`, this variant doesn't require `DifferentiableAt`
because it doesn't take individual derivatives.
-/
lemma fderiv_eq_copi_fderiv_comp_single {f : (‚àÄ i, M i) ‚Üí F} {x : (‚àÄ i, M i)} :
    fderiv ùïú f x = ContinuousLinearMap.copi (fun i => (fderiv ùïú f x).comp (ContinuousLinearMap.single i)) := by
  rw [ContinuousLinearMap.copi_comp_single]

/-- The Fr√©chet derivative can be rewritten as a `ContinuousLinearMap.copi` of partial derivatives. -/
lemma fderiv_eq_copi_fpideriv {f : (‚àÄ i, M i) ‚Üí F} {x : (‚àÄ i, M i)}
    (hf : DifferentiableAt ùïú f x) :
    fderiv ùïú f x = ContinuousLinearMap.copi (fun i => fpideriv ùïú i f x) := by
  rw [fderiv_eq_copi_fderiv_comp_single]
  -- conv => lhs; rw [‚Üê ContinuousLinearMap.copi_comp_single (f := fderiv ùïú f x)]
  congr
  ext i m
  simp
  exact (fpideriv_apply_eq_fderiv_apply_single hf).symm

-- This can be rewritten using `smul_pideriv`, `fderiv_apply_single`.
/-- The Fr√©chet derivative can be rewritten as a sum of partial derivatives. -/
lemma fderiv_apply_eq_sum_fpideriv {f : (‚àÄ i, M i) ‚Üí F} {x m : (‚àÄ i, M i)}
    (hf : DifferentiableAt ùïú f x) :
    fderiv ùïú f x m = ‚àë i : Œπ, fpideriv ùïú i f x (m i) := by
  rw [fderiv_eq_copi_fpideriv hf]
  rfl

lemma fderiv_apply_eq_sum_fderiv_apply_single {f : (‚àÄ i, M i) ‚Üí F} {x m : ‚àÄ i, M i} :
    fderiv ùïú f x m = ‚àë i : Œπ, fderiv ùïú f x (Pi.single i (m i)) := by
  conv => lhs; rw [fderiv_eq_copi_fderiv_comp_single]


-- /-- The Fr√©chet derivative can be written as a sum of partial derivatives over coordinates. -/
-- lemma fderiv_apply_eq_sum_smul_ideriv {f : (Œπ ‚Üí ùïú) ‚Üí F} {x dx : Œπ ‚Üí ùïú} (hf : DifferentiableAt ùïú f x) :
--     fderiv ùïú f x dx = ‚àë i, dx i ‚Ä¢ pideriv i f x := by
--   change (fderiv ùïú f x).toLinearMap dx = _
--   rw [LinearMap.pi_apply_eq_sum_univ]
--   congr
--   ext i
--   rw [ideriv_eq_fderiv_single hf]
--   simp_rw [‚Üê Pi.single_apply]
--   simp_rw [Pi.single_comm]
--   rfl


-- TODO: `hasFDerivAt_iff_hasPiDerivAt_forall`

-- lemma hasFDerivAt_copi_iff_hasFPiDerivAt_forall {f : (‚àÄ i, M i) ‚Üí F} {f' : ‚àÄ i, M i ‚ÜíL[ùïú] F} {x : ‚àÄ i, M i} :
--     HasFDerivAt f (ContinuousLinearMap.copi f') x ‚Üî ‚àÄ i, HasFPiDerivAt i f (f' i) x := by
--   simp [HasFPiDerivAt]
--   sorry

--   -- rw [HasFDerivAt]
--   -- rw [HasFDerivAtFilter]

--   -- simp [HasFPiDerivAt]
--   -- refine Iff.intro ?_ ?_
--   -- . sorry
--   -- intro h
--   -- sorry

--   -- rw [hasFDerivAt_iff_hasPiDerivAt]
--   -- simp [Pi.single_comm]

-- lemma hasFDerivAt_copi_iff_hasPiDerivAt_forall {f : (Œπ ‚Üí ùïú) ‚Üí F} {f' : Œπ ‚Üí ùïú ‚ÜíL[ùïú] F} {x : Œπ ‚Üí ùïú} :
--     HasFDerivAt f (ContinuousLinearMap.copi f') x ‚Üî ‚àÄ i, HasPiDerivAt i f (f' i) (x i) := by
--   sorry
--   rw [hasFDerivAt_iff_hasPiDerivAt]
--   simp [Pi.single_comm]


-- lemma differentiableAt_comp_update {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú}
--     (hf : DifferentiableAt ùïú f x) :
--     DifferentiableAt ùïú (fun xi => f (Function.update x i xi)) (x i) := by
--   change DifferentiableAt ùïú (f ‚àò Function.update x i) (x i)
--   refine DifferentiableAt.comp _ ?_ (hasFDerivAt_update x _).differentiableAt
--   simp
--   exact hf


-- -- TODO: Is there some lemma for the norm of an lsum?
-- -- TODO: Upgrade to a CLM?
-- lemma fderiv_toLinearMap_eq_lsum_fpideriv {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú} (hf : DifferentiableAt ùïú f x) :
--     (fderiv ùïú f x).toLinearMap = LinearMap.lsum ùïú (fun _ : Œπ => ùïú) ùïú (fun i => (fpideriv ùïú i f x).toLinearMap) := by
--   refine LinearMap.ext ?_
--   intro dx
--   simp
--   change fderiv ùïú f x dx = _
--   rw [fderiv_pi_apply_eq_sum]
--   congr
--   ext i
--   rw [‚Üê ideriv_eq_fderiv_single hf]
--   rw [fpideriv_eq_smul_ideriv]


/--
The norm of the fderiv map is bounded by norms of the coordinate derivatives.

TODO: Establish an equality?
TODO: Could eliminate `DifferentiableAt` using `Or.elim`.
-/
lemma norm_fderiv_le_sum_norm_fpideriv {f : (‚àÄ i, M i) ‚Üí F} {x : ‚àÄ i, M i} (hf : DifferentiableAt ùïú f x) :
    ‚Äñfderiv ùïú f x‚Äñ ‚â§ ‚àë i, ‚Äñfpideriv ùïú i f x‚Äñ := by
  rw [ContinuousLinearMap.opNorm_le_iff (Finset.sum_nonneg (fun i _ => norm_nonneg _))]
  intro u
  rw [fderiv_apply_eq_sum_fpideriv hf]
  refine le_trans (norm_sum_le _ _) ?_
  rw [Finset.sum_mul]
  refine Finset.sum_le_sum ?_
  simp
  intro i
  refine le_trans (ContinuousLinearMap.le_opNorm _ _) ?_
  exact mul_le_mul_of_nonneg_left (norm_le_pi_norm u i) (norm_nonneg _)

/-- The norm of the fderiv map is bounded by norms of the coordinate derivatives. -/
lemma norm_fderiv_le_sum_norm_pideriv {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú} (hf : DifferentiableAt ùïú f x) :
    ‚Äñfderiv ùïú f x‚Äñ ‚â§ ‚àë i, ‚Äñpideriv i f x‚Äñ := by
  refine le_trans (norm_fderiv_le_sum_norm_fpideriv hf) ?_
  refine Finset.sum_le_sum ?_
  simp
  intro i
  rw [ContinuousLinearMap.opNorm_le_iff (norm_nonneg _)]
  intro m
  refine le_of_eq ?_
  rw [fpideriv_apply_eq_smul_pideriv]
  rw [norm_smul]
  ring_nf


lemma norm_fpideriv_le_norm_fderiv {i : Œπ} {f : (‚àÄ i, M i) ‚Üí F} {x : ‚àÄ i, M i} (hf : DifferentiableAt ùïú f x) :
    ‚Äñfpideriv ùïú i f x‚Äñ ‚â§ ‚Äñfderiv ùïú f x‚Äñ := by
  simp_rw [fpideriv_eq_fderiv_comp_single hf]
  refine le_trans (ContinuousLinearMap.opNorm_comp_le _ _) ?_
  exact mul_le_of_le_one_right (norm_nonneg _) ContinuousLinearMap.norm_single_le_one

lemma norm_pideriv_le_norm_fderiv {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú} (hf : DifferentiableAt ùïú f x) :
    ‚Äñpideriv i f x‚Äñ ‚â§ ‚Äñfderiv ùïú f x‚Äñ := by
  simp_rw [pideriv_eq_fderiv_apply_single hf]
  refine le_trans (ContinuousLinearMap.le_opNorm _ _) ?_
  simp

lemma norm_iteratedFDeriv_fpideriv_le_norm_iteratedFDeriv_succ {n : ‚Ñï} {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú}
    (hf : ContDiff ùïú (n + 1) f) :
    ‚ÄñiteratedFDeriv ùïú n (fpideriv ùïú i f) x‚Äñ ‚â§ ‚ÄñiteratedFDeriv ùïú (n + 1) f x‚Äñ := by
  change ‚ÄñiteratedFDeriv ùïú n (fun y => fpideriv ùïú i f y) x‚Äñ ‚â§ _
  have hf_diff {y} : DifferentiableAt ùïú f y := hf.differentiable (by norm_num) _
  simp_rw [fpideriv_eq_fderiv_comp_single hf_diff]
  refine le_trans (norm_iteratedFDeriv_clm_comp_const (hf.fderiv_right (le_refl _))) ?_
  rw [norm_iteratedFDeriv_fderiv]
  simp
  refine mul_le_of_le_one_left (norm_nonneg _) ?_
  exact ContinuousLinearMap.norm_single_le_one

-- TODO: Could use `norm_iteratedFDeriv_fpideriv_le_norm_iteratedFDeriv_succ`.

lemma norm_iteratedFDeriv_pideriv_le_norm_iteratedFDeriv_succ {n : ‚Ñï} {i : Œπ} {f : (Œπ ‚Üí ùïú) ‚Üí F} {x : Œπ ‚Üí ùïú}
    (hf : ContDiff ùïú (n + 1) f) :
    ‚ÄñiteratedFDeriv ùïú n (pideriv i f) x‚Äñ ‚â§ ‚ÄñiteratedFDeriv ùïú (n + 1) f x‚Äñ := by
  change ‚ÄñiteratedFDeriv ùïú n (fun y => pideriv i f y) x‚Äñ ‚â§ _
  have hf_diff {y} : DifferentiableAt ùïú f y := hf.differentiable (by norm_num) _
  simp_rw [pideriv_eq_fderiv_apply_single hf_diff]
  refine le_trans (norm_iteratedFDeriv_clm_apply_const (hf.fderiv_right (le_refl _)) (le_refl _)) ?_
  simp
  rw [norm_iteratedFDeriv_fderiv]


-- /-- Second derivative is symmetric; that is, `u' H v = v' H u`. -/
-- lemma fderiv_fderiv_symm {f : (‚àÄ i, M i) ‚Üí F} {x : ‚àÄ i, M i} {u v : ‚àÄ i, M i}
--     (hf : DifferentiableAt ùïú f x) (hf' : DifferentiableAt ùïú (fderiv ùïú f) x) :
--     fderiv ùïú (fderiv ùïú f) x u v = fderiv ùïú (fderiv ùïú f) x v u := by
--   change fderiv ùïú (fun y => fderiv ùïú f y) x u v = fderiv ùïú (fun y => fderiv ùïú f y) x v u
--   rw [‚Üê fderiv_clm_apply_comm hf']
--   rw [‚Üê fderiv_clm_apply_comm hf']
--   rw [fderiv_apply_eq_sum_fderiv_apply_single]
--   rw [fderiv_apply_eq_sum_fderiv_apply_single]
--   simp_rw [fderiv_apply_eq_sum_fderiv_apply_single (m := v)]
--   simp_rw [fderiv_apply_eq_sum_fderiv_apply_single (m := u)]
--   rw [fderiv_sum (fun i _ => DifferentiableAt.clm_apply hf' (differentiableAt_const _))]
--   rw [fderiv_sum (fun i _ => DifferentiableAt.clm_apply hf' (differentiableAt_const _))]
--   conv => lhs; arg 2; intro i; rw [ContinuousLinearMap.sum_apply]
--   conv => rhs; arg 2; intro i; rw [ContinuousLinearMap.sum_apply]
--   rw [Finset.sum_comm]
--   conv => lhs; arg 2; intro j; arg 2; intro i
--   conv => rhs; arg 2; intro j; arg 2; intro i
--   refine Finset.sum_congr rfl ?_
--   intro i _
--   refine Finset.sum_congr rfl ?_
--   intro j _
--   -- This just reduced to the partial derivative problem.
--   sorry


-- /-- Partial derivatives commute. -/
-- lemma fpideriv_comm {i‚ÇÅ i‚ÇÇ : Œπ} {f : (‚àÄ j, M j) ‚Üí F} {x : (‚àÄ j, M j)} {m‚ÇÅ : M i‚ÇÅ} {m‚ÇÇ : M i‚ÇÇ}
--     (hf : Differentiable ùïú f)
--     (hf' : Differentiable ùïú (fun x => fderiv ùïú f x)) :
--     fpideriv ùïú i‚ÇÅ (fun x => fpideriv ùïú i‚ÇÇ f x m‚ÇÇ) x m‚ÇÅ =
--     fpideriv ùïú i‚ÇÇ (fun x => fpideriv ùïú i‚ÇÅ f x m‚ÇÅ) x m‚ÇÇ := by
--   have (i‚ÇÅ i‚ÇÇ m‚ÇÅ m‚ÇÇ) : fpideriv ùïú i‚ÇÅ (fun x => fpideriv ùïú i‚ÇÇ f x m‚ÇÇ) x m‚ÇÅ =
--       fpideriv ùïú i‚ÇÅ (fun x => fderiv ùïú f x (Pi.single i‚ÇÇ m‚ÇÇ)) x m‚ÇÅ
--   . congr
--     ext y
--     rw [fpideriv_eq_fderiv_comp_single (hf y)]
--     rw [ContinuousLinearMap.comp_apply]
--     simp
--   simp [this]
--   clear this

--   have (i‚ÇÅ i‚ÇÇ m‚ÇÅ m‚ÇÇ) : fpideriv ùïú i‚ÇÅ (fun x => fderiv ùïú f x (Pi.single i‚ÇÇ m‚ÇÇ)) x m‚ÇÅ =
--       fderiv ùïú (fun x => fderiv ùïú f x) x (Pi.single i‚ÇÅ m‚ÇÅ) (Pi.single i‚ÇÇ m‚ÇÇ)
--   . rw [fpideriv_eq_fderiv_comp_single ((hf' x).clm_apply (differentiableAt_const _))]
--     rw [ContinuousLinearMap.comp_apply]
--     simp
--     rw [fderiv_clm_apply_const (hf' x)]
--     simp
--   simp [this]
--   clear this

--   -- rw [fpideriv_eq_fderiv_comp_single ((hf' x).clm_apply (differentiableAt_const _))]
--   -- rw [ContinuousLinearMap.comp_apply]
--   -- simp
--   -- rw [fderiv_clm_apply_const (hf' x)]
--   -- simp

--   sorry


noncomputable def orderedIteratedPiDerivAux {n : ‚Ñï} (f : (Œπ ‚Üí ùïú) ‚Üí F) : (Fin n ‚Üí Œπ) ‚Üí (Œπ ‚Üí ùïú) ‚Üí F :=
  Nat.recOn n
    (fun _ x => f x)
    (fun _n rec js x => pideriv (js 0) (fun y => rec (Fin.tail js) y) x)

noncomputable def orderedIteratedPiDeriv {n : ‚Ñï} (js : Fin n ‚Üí Œπ) (f : (Œπ ‚Üí ùïú) ‚Üí F) (x : Œπ ‚Üí ùïú) : F :=
  orderedIteratedPiDerivAux f js x

section Def
variable (ùïú)

noncomputable def orderedIteratedFPiDerivAux {n : ‚Ñï} (f : (‚àÄ i, M i) ‚Üí F) :
    (js : Fin n ‚Üí Œπ) ‚Üí (‚àÄ i, M i) ‚Üí ContinuousMultilinearMap ùïú (fun i : Fin n => M (js i)) F :=
  Nat.recOn n
    (fun js x => ContinuousMultilinearMap.constOfIsEmpty ùïú (fun i : Fin 0 => M (js i)) (f x))
    (fun _n rec js x => ContinuousLinearMap.uncurryLeft (fpideriv ùïú (js 0) (fun y => rec (Fin.tail js) y) x))

noncomputable def orderedIteratedFPiDeriv {n : ‚Ñï} (js : Fin n ‚Üí Œπ) (f : (‚àÄ i, M i) ‚Üí F) (x : ‚àÄ i, M i) :
    ContinuousMultilinearMap ùïú (fun i : Fin n => M (js i)) F :=
  orderedIteratedFPiDerivAux ùïú f js x

end Def

@[simp]
lemma orderedIteratedPiDeriv_zero {f : (Œπ ‚Üí ùïú) ‚Üí F} {js : Fin 0 ‚Üí Œπ} {x : Œπ ‚Üí ùïú} :
    orderedIteratedPiDeriv js f x = f x := rfl

@[simp]
lemma orderedIteratedPiDeriv_one {f : (Œπ ‚Üí ùïú) ‚Üí F} {js : Fin 1 ‚Üí Œπ} {x : Œπ ‚Üí ùïú} :
    orderedIteratedPiDeriv js f x = pideriv (js 0) f x := rfl

lemma orderedIteratedPiDeriv_succ_left {n : ‚Ñï} {f : (Œπ ‚Üí ùïú) ‚Üí F} {js : Fin (n + 1) ‚Üí Œπ} {x : Œπ ‚Üí ùïú} :
    orderedIteratedPiDeriv js f x = pideriv (js 0) (orderedIteratedPiDeriv (Fin.tail js) f) x := rfl

lemma orderedIteratedPiDeriv_succ_right {n : ‚Ñï} {f : (Œπ ‚Üí ùïú) ‚Üí F} {js : Fin (n + 1) ‚Üí Œπ} {x : Œπ ‚Üí ùïú} :
    orderedIteratedPiDeriv js f x = orderedIteratedPiDeriv (Fin.init js) (pideriv (js (Fin.last n)) f) x := by
  induction n generalizing f x with
  | zero => rfl
  | succ n ih =>
    -- Apply inductive hypothesis to elements `1 .. n + 2`.
    have A (y) : orderedIteratedPiDeriv (Fin.tail js) f y =
        orderedIteratedPiDeriv (Fin.init (Fin.tail js)) (pideriv (js (Fin.last (n + 1))) f) y
    . rw [ih]
      rfl
    rw [orderedIteratedPiDeriv_succ_left]
    change pideriv (js 0) (fun y => orderedIteratedPiDeriv (Fin.tail js) f y) x = _
    simp_rw [A]
    rfl


lemma orderedIteratedFPiDeriv_zero_apply {f : (‚àÄ i, M i) ‚Üí F} {js : Fin 0 ‚Üí Œπ} {x : ‚àÄ i, M i} {m : ‚àÄ i, M (js i)} :
    orderedIteratedFPiDeriv ùïú js f x m = f x := rfl

theorem orderedIteratedFPiDeriv_zero_eq_comp {f : (‚àÄ i, M i) ‚Üí F} {js : Fin 0 ‚Üí Œπ} :
    orderedIteratedFPiDeriv ùïú js f = (continuousMultilinearIsEmptyEquiv ùïú (fun i : Fin 0 => M (js i)) F).symm ‚àò f := rfl

theorem orderedIteratedFPiDeriv_succ_eq_comp_left {f : (‚àÄ i, M i) ‚Üí F} {js : Fin (n + 1) ‚Üí Œπ} :
    orderedIteratedFPiDeriv ùïú js f =
    ‚áë(continuousMultilinearCurryLeftEquiv ùïú (fun i : Fin (n + 1) => M (js i)) F) ‚àò
      (fpideriv ùïú (js 0) (orderedIteratedFPiDeriv ùïú (Fin.tail js) f)) := rfl

theorem orderedIteratedFPiDeriv_succ_apply_left {n : ‚Ñï} {f : (‚àÄ i, M i) ‚Üí F} {js : Fin (n + 1) ‚Üí Œπ} {x : ‚àÄ i, M i}
    {m : (i : Fin (n + 1)) ‚Üí M (js i)} :
    orderedIteratedFPiDeriv ùïú js f x m =
    fpideriv ùïú (js 0) (orderedIteratedFPiDeriv ùïú (Fin.tail js) f) x (m 0) (Fin.tail m) := rfl

theorem orderedIteratedFPiDeriv_succ_apply_right {n : ‚Ñï} {f : (‚àÄ i, M i) ‚Üí F} {js : Fin (n + 1) ‚Üí Œπ} {x : ‚àÄ i, M i}
    {m : (i : Fin (n + 1)) ‚Üí M (js i)} :
    orderedIteratedFPiDeriv ùïú js f x m =
    orderedIteratedFPiDeriv ùïú (Fin.init js) (fpideriv ùïú (js (Fin.last n)) f) x (Fin.init m) (m (Fin.last n)) := by
  simp  -- `ContinuousLinearMap.strongUniformity_topology_eq`?
  -- Follows `iteratedFDerivWithin_succ_apply_right`.
  induction n generalizing x with
  | zero =>
    rw [orderedIteratedFPiDeriv_succ_eq_comp_left]
    rw [fpideriv_def, orderedIteratedFPiDeriv_zero_eq_comp]
    rw [Function.comp_apply, continuousMultilinearCurryLeftEquiv_apply]
    change fderiv ùïú (‚áë(LinearIsometryEquiv.symm (continuousMultilinearIsEmptyEquiv ùïú (fun i => M (Fin.tail js i)) F)) ‚àò
      f ‚àò (Function.update x (js 0))) _ _ _ = _
    rw [LinearIsometryEquiv.comp_fderiv]
    rw [ContinuousLinearMap.comp_apply]
    -- simp
    rfl
  | succ n ih =>
    -- Apply inductive hypothesis to `n + 1` elements `1 .. n + 2` and introduce equiv.
    -- Then expand on the left, where things are definitionally equal.
    have A (y) : orderedIteratedFPiDeriv ùïú (Fin.tail js) f y =
        (continuousMultilinearCurryRightEquiv ùïú (fun i : Fin (n + 1) => M (js (Fin.succ i))) F ‚àò
          orderedIteratedFPiDeriv ùïú (Fin.init (Fin.tail js)) (fpideriv ùïú (js (Fin.last (n + 1))) f)) y
    . ext m
      rw [ih]
      rfl
    rw [orderedIteratedFPiDeriv_succ_eq_comp_left]
    rw [Function.comp_apply, continuousMultilinearCurryLeftEquiv_apply]
    rw [fpideriv_def]
    simp
    simp_rw [A]
    change fderiv ùïú (
      ‚áë(continuousMultilinearCurryRightEquiv ùïú (fun i => M (js (Fin.succ i))) F) ‚àò
      (orderedIteratedFPiDeriv ùïú (Fin.init (Fin.tail js)) (fpideriv ùïú (js (Fin.last (n + 1))) f) ‚àò Function.update x (js 0)))
      _ _ _ = _
    rw [LinearIsometryEquiv.comp_fderiv]
    -- simp
    -- rw [continuousMultilinearCurryRightEquiv_apply]  -- `Fin.castSucc` vs `Fin.succ`
    rfl

theorem orderedIteratedFPiDeriv_succ_eq_comp_right {f : (‚àÄ i, M i) ‚Üí F} {js : Fin (n + 1) ‚Üí Œπ} :
    orderedIteratedFPiDeriv ùïú js f =
      ‚áë(continuousMultilinearCurryRightEquiv ùïú (fun i : Fin (n + 1) => M (js i)) F) ‚àò
      (orderedIteratedFPiDeriv ùïú (Fin.init js) (fpideriv ùïú (js (Fin.last n)) f)) := by
  ext x m
  rw [orderedIteratedFPiDeriv_succ_apply_right]
  rfl

theorem orderedIteratedFPiDeriv_one_apply {f : (‚àÄ i, M i) ‚Üí F} {js : Fin 1 ‚Üí Œπ} {x : ‚àÄ i, M i} {m : ‚àÄ i, M (js i)} :
    orderedIteratedFPiDeriv ùïú js f x m = fpideriv ùïú (js 0) f x (m 0) := by
  rw [orderedIteratedFPiDeriv_succ_apply_right]
  rw [orderedIteratedFPiDeriv_zero_apply]
  rfl


-- noncomputable instance {n : ‚Ñï} {js : Fin (n + 2) ‚Üí Œπ} :
--     NormedAddCommGroup (M (Fin.tail js 0) ‚ÜíL[ùïú] ContinuousMultilinearMap ùïú (fun _ : Fin n => (i : Œπ) ‚Üí M i) F) :=
--   ContinuousLinearMap.toNormedAddCommGroup

-- noncomputable instance {n : ‚Ñï} {js : Fin (n + 2) ‚Üí Œπ} :
--     NormedSpace ùïú (M (Fin.tail js 0) ‚ÜíL[ùïú] ContinuousMultilinearMap ùïú (fun _ : Fin n => (i : Œπ) ‚Üí M i) F) :=
--   ContinuousLinearMap.toNormedSpace

-- noncomputable instance {n : ‚Ñï} :
--     NormedAddCommGroup (((i : Œπ) ‚Üí M i) ‚ÜíL[ùïú] ContinuousMultilinearMap ùïú (fun _ : Fin n => (i : Œπ) ‚Üí M i) F) :=
--   ContinuousLinearMap.toNormedAddCommGroup

-- noncomputable instance {n : ‚Ñï} :
--     NormedSpace ùïú (((i : Œπ) ‚Üí M i) ‚ÜíL[ùïú] ContinuousMultilinearMap ùïú (fun _ : Fin n => (i : Œπ) ‚Üí M i) F) :=
--   ContinuousLinearMap.toNormedSpace


lemma ContinuousMultilinearMap.curryLeft_compContinuousLinearMap
    {M‚ÇÅ : Fin (n + 1) ‚Üí Type*}
    [‚àÄ i, NormedAddCommGroup (M‚ÇÅ i)] [‚àÄ i, NormedSpace ùïú (M‚ÇÅ i)]
    {M‚ÇÅ' : Fin (n + 1) ‚Üí Type*}
    [‚àÄ i, NormedAddCommGroup (M‚ÇÅ' i)] [‚àÄ i, NormedSpace ùïú (M‚ÇÅ' i)]
    (g : ContinuousMultilinearMap ùïú M‚ÇÅ' F)
    (f : ‚àÄ i, M‚ÇÅ i ‚ÜíL[ùïú] M‚ÇÅ' i) :
    (ContinuousMultilinearMap.compContinuousLinearMap g f).curryLeft =
    -- ((ContinuousMultilinearMap.compContinuousLinearMapL (fun i => f (Fin.succ i))).comp g.curryLeft).comp (f 0) := by
    (ContinuousMultilinearMap.compContinuousLinearMapL (fun i => f (Fin.succ i))).comp (g.curryLeft.comp (f 0)) := by
  ext u m
  simp
  congr
  ext i
  refine Fin.cases ?_ ?_ i <;> simp


@[simp]
lemma continuousMultilinearCurryLeftEquiv_curryLeft
    {M‚ÇÅ : Fin (n + 1) ‚Üí Type*}
    [‚àÄ i, NormedAddCommGroup (M‚ÇÅ i)] [‚àÄ i, NormedSpace ùïú (M‚ÇÅ i)]
    (g : ContinuousMultilinearMap ùïú M‚ÇÅ F) :
    (continuousMultilinearCurryLeftEquiv ùïú M‚ÇÅ F) (g.curryLeft) = g := by
  ext x
  simp


section FormalMultilinearSeries

variable {f : (‚àÄ i, M i) ‚Üí F} {f' : ((i : Œπ) ‚Üí M i) ‚Üí FormalMultilinearSeries ùïú ((i : Œπ) ‚Üí M i) F}

theorem HasFTaylorSeriesUpTo.hasFDerivAt_orderedIteratedFPiDeriv' {n : ‚Ñï} (hf : HasFTaylorSeriesUpTo (n + 1) f f')
    {js : Fin n ‚Üí Œπ} {x : ‚àÄ i, M i} :
    HasFDerivAt (orderedIteratedFPiDeriv ùïú js f)
      ((ContinuousMultilinearMap.compContinuousLinearMapL (fun i => ContinuousLinearMap.single (js i))).comp
        (f' x (n + 1)).curryLeft) x := by
  induction n generalizing x with
  | zero =>
    rw [orderedIteratedFPiDeriv_zero_eq_comp]
    rw [LinearIsometryEquiv.comp_hasFDerivAt_iff']
    refine HasFDerivAt.congr_fderiv (hf.hasFDerivAt (by simp) x) ?_
    ext m
    simp only [continuousMultilinearCurryFin1_apply, Nat.zero_eq, LinearIsometryEquiv.symm_symm,
      ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe, LinearIsometryEquiv.coe_coe,
      Function.comp_apply, ContinuousMultilinearMap.compContinuousLinearMapL_apply,
      continuousMultilinearIsEmptyEquiv_apply,
      ContinuousMultilinearMap.compContinuousLinearMap_apply, Pi.zero_apply, map_zero,
      ContinuousMultilinearMap.curryLeft_apply]
    congr
    funext i
    rw [Fin.eq_zero i]
    simp only [Fin.cons_zero]
    have : (0 : Fin 1) = Fin.last 0 := rfl
    simp [this]
  | succ n ih =>
    rw [‚Üê hasFTaylorSeriesUpToOn_univ_iff] at hf
    rw [hasFTaylorSeriesUpToOn_succ_iff_left] at hf
    rcases hf with ‚ü®hf_pred, hf_diff, _‚ü©
    rw [hasFTaylorSeriesUpToOn_univ_iff] at hf_pred
    simp at hf_diff
    specialize @ih hf_pred
    rw [orderedIteratedFPiDeriv_succ_eq_comp_left]
    -- Use inductive hypothesis to rewrite `fpideriv`.
    conv =>
      arg 1; intro y; arg 2; intro x
      rw [fpideriv_eq_fderiv_comp_single ih.differentiableAt]
      rw [ih.fderiv]
      rw [ContinuousLinearMap.comp_assoc]
      simp [Fin.tail]
      rw [‚Üê ContinuousMultilinearMap.curryLeft_compContinuousLinearMap (f' x (n + 1)) (fun i => ContinuousLinearMap.single (js i))]
    simp
    simp_rw [‚Üê ContinuousMultilinearMap.compContinuousLinearMapL_apply]
    refine ((ContinuousLinearMap.hasFDerivAt _).comp _ (hf_diff _)).congr_fderiv ?_
    ext m v
    simp


theorem HasFTaylorSeriesUpTo.hasFDerivAt_orderedIteratedFPiDeriv {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n < N)
    (hf : HasFTaylorSeriesUpTo N f f') {js : Fin n ‚Üí Œπ} {x : ‚àÄ i, M i} :
    HasFDerivAt (orderedIteratedFPiDeriv ùïú js f)
      ((ContinuousMultilinearMap.compContinuousLinearMapL (fun i => ContinuousLinearMap.single (js i))).comp
        (f' x (n + 1)).curryLeft) x :=
  hasFDerivAt_orderedIteratedFPiDeriv' (hf.ofLe (ENat.add_one_le_of_lt hn))


theorem HasFTaylorSeriesUpTo.fderiv_orderedIteratedFPiDeriv {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n < N)
    (hf : HasFTaylorSeriesUpTo N f f') {js : Fin n ‚Üí Œπ} {x : ‚àÄ i, M i} :
    _root_.fderiv ùïú (orderedIteratedFPiDeriv ùïú js f) x =
    ((ContinuousMultilinearMap.compContinuousLinearMapL (fun i => ContinuousLinearMap.single (js i))).comp (f' x (n + 1)).curryLeft) :=
  (hasFDerivAt_orderedIteratedFPiDeriv hn hf).fderiv

theorem HasFTaylorSeriesUpTo.differentiableAt_orderedIteratedFPiDeriv {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n < N)
    (hf : HasFTaylorSeriesUpTo N f f') {js : Fin n ‚Üí Œπ} {x : ‚àÄ i, M i} :
    DifferentiableAt ùïú (orderedIteratedFPiDeriv ùïú js f) x :=
  (hasFDerivAt_orderedIteratedFPiDeriv hn hf).differentiableAt

theorem HasFTaylorSeriesUpTo.differentiable_orderedIteratedFPiDeriv {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n < N)
    (hf : HasFTaylorSeriesUpTo N f f') {js : Fin n ‚Üí Œπ} :
    Differentiable ùïú (orderedIteratedFPiDeriv ùïú js f) :=
  fun _ => (hasFDerivAt_orderedIteratedFPiDeriv hn hf).differentiableAt


theorem HasFTaylorSeriesUpTo.orderedIteratedFPiDeriv_apply {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n ‚â§ N)
    (hf : HasFTaylorSeriesUpTo N f f') {js : Fin n ‚Üí Œπ} {x : ‚àÄ i, M i} {m : (i : Fin n) ‚Üí M (js i)} :
    (orderedIteratedFPiDeriv ùïú js f x) m = (f' x n) (fun i => Pi.single (js i) (m i)) := by
  cases n with
  | zero =>
    simp
    rw [orderedIteratedFPiDeriv_zero_apply]
    rw [Subsingleton.elim (fun i => Pi.single (js i) _) 0]
    rw [‚Üê ContinuousMultilinearMap.uncurry0_apply]
    rw [hf.zero_eq]
  | succ n =>
    -- TODO: Induction not required; can this be further simplified?
    replace hn : n < N
    . refine lt_of_lt_of_le ?_ hn
      norm_cast
      exact Nat.lt_succ_self n
    rw [orderedIteratedFPiDeriv_succ_apply_left]
    rw [fpideriv_apply_eq_fderiv_apply_single (differentiableAt_orderedIteratedFPiDeriv hn hf)]
    rw [HasFTaylorSeriesUpTo.fderiv_orderedIteratedFPiDeriv hn hf]
    rw [ContinuousLinearMap.comp_apply]
    simp only [ContinuousMultilinearMap.compContinuousLinearMapL_apply,
      ContinuousMultilinearMap.compContinuousLinearMap_apply, ContinuousLinearMap.single_apply,
      ContinuousMultilinearMap.curryLeft_apply]
    congr
    simp only [Fin.tail]
    funext i
    refine Fin.cases ?_ ?_ i <;> simp


theorem HasFTaylorSeriesUpTo.orderedIteratedFPiDeriv_eq_compContinuousLinearMap {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n ‚â§ N)
    (hf : HasFTaylorSeriesUpTo N f f') {js : Fin n ‚Üí Œπ} {x : ‚àÄ i, M i} :
    orderedIteratedFPiDeriv ùïú js f x = (f' x n).compContinuousLinearMap (fun i => ContinuousLinearMap.single (js i)) := by
  ext m
  rw [orderedIteratedFPiDeriv_apply hn hf]
  rfl


lemma ContDiff.differentiable_orderedIteratedFPiDeriv {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n < N) (hf : ContDiff ùïú N f)
    {js : Fin n ‚Üí Œπ} :
    Differentiable ùïú (orderedIteratedFPiDeriv ùïú js f) := by
  rcases hf with ‚ü®f', hf‚ü©
  exact hf.differentiable_orderedIteratedFPiDeriv hn

lemma ContDiff.differentiableAt_orderedIteratedFPiDeriv {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n < N) (hf : ContDiff ùïú N f)
    {js : Fin n ‚Üí Œπ} {x : ‚àÄ i, M i} :
    DifferentiableAt ùïú (orderedIteratedFPiDeriv ùïú js f) x := by
  rcases hf with ‚ü®f', hf‚ü©
  exact hf.differentiableAt_orderedIteratedFPiDeriv hn


/-- The iterated partial derivative is equal to the iterated derivative applied to one-hot vectors. -/
theorem ContDiff.orderedIteratedFPiDeriv_apply_eq_iteratedFDeriv_apply_single {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n ‚â§ N) (hf : ContDiff ùïú N f)
    {js : Fin n ‚Üí Œπ} {x : ‚àÄ i, M i} {m : ‚àÄ k, M (js k)} :
    orderedIteratedFPiDeriv ùïú js f x m = iteratedFDeriv ùïú n f x (fun k => Pi.single (js k) (m k)) := by
  rw [contDiff_iff_ftaylorSeries] at hf
  rw [hf.orderedIteratedFPiDeriv_apply hn]
  rfl

theorem ContDiff.orderedIteratedFPiDeriv_eq_iteratedFDeriv_comp_single {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n ‚â§ N) (hf : ContDiff ùïú N f)
    {js : Fin n ‚Üí Œπ} {x : ‚àÄ i, M i} :
    orderedIteratedFPiDeriv ùïú js f x = (iteratedFDeriv ùïú n f x).compContinuousLinearMap (fun i => ContinuousLinearMap.single (js i)) := by
  ext m
  rw [orderedIteratedFPiDeriv_apply_eq_iteratedFDeriv_apply_single hn hf]
  rfl

-- Proof follows `ContDiffWithinAt.iteratedFderivWithin_right`.
-- TODO: Is there an easier way to write this, like `ContDiff.orderedIteratedPiDeriv_right`?
/-- The iterated partial derivative is continuous differentiable. -/
theorem ContDiff.orderedIteratedFPiDeriv_right {m N : ‚Ñï‚àû} {n : ‚Ñï} (hn : m + n ‚â§ N) (hf : ContDiff ùïú N f) {js : Fin n ‚Üí Œπ} :
    ContDiff ùïú m (_root_.orderedIteratedFPiDeriv ùïú js f) := by
  induction n generalizing m with
  | zero =>
    rw [orderedIteratedFPiDeriv_zero_eq_comp]
    simp at hn
    exact (LinearIsometryEquiv.contDiff _).comp (hf.of_le hn)
  | succ n ih =>
    rw [orderedIteratedFPiDeriv_succ_eq_comp_left]
    refine ContDiff.comp ?_ ?_
    . exact LinearIsometryEquiv.contDiff _  -- Can't put inline for some reason?
    have hn' : n < N  -- TODO: Is there a simpler way to obtain this?
    . simp at hn
      rw [‚Üê add_assoc] at hn
      refine lt_of_lt_of_le ?_ hn
      rw [‚Üê ENat.add_one_le_iff (ENat.coe_ne_top n)]
      rw [add_assoc]
      exact le_add_self
    conv =>
      arg 3; intro x
      rw [fpideriv_eq_fderiv_comp_single (hf.differentiable_orderedIteratedFPiDeriv hn' _)]
    refine ContDiff.clm_comp ?_ contDiff_const
    specialize @ih (m + 1) ?_
    . rw [add_assoc]
      rw [add_comm 1]
      exact hn
    exact ContDiff.fderiv_right ih (le_refl _)

end FormalMultilinearSeries


section FormalMultilinearSeriesField

variable {f : (Œπ ‚Üí ùïú) ‚Üí F} {f' : (Œπ ‚Üí ùïú) ‚Üí FormalMultilinearSeries ùïú (Œπ ‚Üí ùïú) F}

/-- Unlike `contDiff_update`, does not use `Classical.propDecidable` (caused problems with typeclasses). -/
lemma contDiff_update' (n : ‚Ñï‚àû) (x : ‚àÄ i, M i) (i : Œπ) : ContDiff ùïú n (Function.update x i) := by
  conv => arg 3; intro u; rw [‚Üê ContinuousLinearMap.updateL_apply (R := ùïú)]
  exact (ContinuousLinearMap.contDiff _).comp (contDiff_prod_mk_right x)

-- Just for convenience.
lemma differentiable_update' (x : ‚àÄ i, M i) (i : Œπ) : Differentiable ùïú (Function.update x i) :=
  (contDiff_update' 1 x i).differentiable (le_refl 1)


theorem ContDiff.orderedIteratedPiDeriv_eq_orderedIteratedFPiDeriv {n : ‚Ñï} (hf : ContDiff ùïú n f) {js : Fin n ‚Üí Œπ} {x : Œπ ‚Üí ùïú} :
    orderedIteratedPiDeriv js f x = orderedIteratedFPiDeriv ùïú js f x 1 := by
  induction n generalizing x with
  | zero => rfl
  | succ n ih =>
    specialize @ih hf.of_succ
    rw [orderedIteratedPiDeriv_succ_left, orderedIteratedFPiDeriv_succ_apply_left]
    rw [‚Üê fpideriv_pideriv]
    simp
    change _ = ((fpideriv ùïú (js 0) (orderedIteratedFPiDeriv ùïú (Fin.tail js) f) x) 1) 1
    rw [fpideriv_apply, fpideriv_apply]
    simp_rw [ih]
    rw [fderiv_continuousMultilinear_apply_const_apply]
    change DifferentiableAt ùïú (orderedIteratedFPiDeriv ùïú (Fin.tail js) f ‚àò Function.update x (js 0)) _
    refine DifferentiableAt.comp _ ?_ ?_
    . refine hf.differentiableAt_orderedIteratedFPiDeriv ?_
      norm_cast
      exact Nat.lt_succ_self n
    . exact differentiable_update' x (js 0) _


theorem ContDiff.orderedIteratedPiDeriv_eq_iteratedFDeriv_apply {n : ‚Ñï} (hf : ContDiff ùïú n f) {js : Fin n ‚Üí Œπ} {x : Œπ ‚Üí ùïú} :
    orderedIteratedPiDeriv js f x = iteratedFDeriv ùïú n f x (fun i => Pi.single (js i) 1) := by
  rw [hf.orderedIteratedPiDeriv_eq_orderedIteratedFPiDeriv]
  rw [hf.orderedIteratedFPiDeriv_apply_eq_iteratedFDeriv_apply_single (le_refl _)]
  rfl


lemma ContDiff.differentiable_orderedIteratedPiDeriv {n : ‚Ñï} {N : ‚Ñï‚àû} (hn : n < N) (hf : ContDiff ùïú N f) {js : Fin n ‚Üí Œπ} :
    Differentiable ùïú (orderedIteratedPiDeriv js f) := by
  conv =>
    arg 2; intro x
    rw [(hf.of_le (le_of_lt hn)).orderedIteratedPiDeriv_eq_orderedIteratedFPiDeriv]
  change Differentiable ùïú fun x => ContinuousMultilinearMap.apply ùïú _ _ 1 (orderedIteratedFPiDeriv ùïú js f x)
  exact (differentiable_const _).clm_apply (hf.differentiable_orderedIteratedFPiDeriv hn)


/-- The iterated partial derivative is continuous differentiable. -/
theorem ContDiff.orderedIteratedPiDeriv_right {m N : ‚Ñï‚àû} {n : ‚Ñï} (hn : m + n ‚â§ N) (hf : ContDiff ùïú N f) {js : Fin n ‚Üí Œπ} :
    ContDiff ùïú m (_root_.orderedIteratedPiDeriv js f) := by
  have hfn : ContDiff ùïú n f := hf.of_le (le_trans (by simp) hn)
  conv => arg 3; intro x; rw [ContDiff.orderedIteratedPiDeriv_eq_iteratedFDeriv_apply hfn]
  exact continuousMultilinear_apply_const (hf.iteratedFDeriv_right hn)

end FormalMultilinearSeriesField

end Derivative
