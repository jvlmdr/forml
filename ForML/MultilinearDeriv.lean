import Mathlib.Analysis.Calculus.ContDiff.Basic

section Induction

-- Fix universes such that `E â†’L[â„] F` and `F` are in the same universe (for induction generalizing F).
universe u
variable {ğ•œ : Type*} {Î± Î² G : Type u}
variable [NontriviallyNormedField ğ•œ]
variable [NormedAddCommGroup Î±] [NormedSpace ğ•œ Î±]
variable [NormedAddCommGroup Î²] [NormedSpace ğ•œ Î²]
variable [NormedAddCommGroup G] [NormedSpace ğ•œ G]

/-- The application of a CLM can be moved outside of `iteratedFDeriv`. -/
theorem iteratedFDeriv_clm_apply_const {n : â„•} {c : Î² â†’ Î± â†’L[ğ•œ] G} {v : Î±} {x : Î²} {m : Fin n â†’ Î²} (hc : ContDiff ğ•œ n c) :
    iteratedFDeriv ğ•œ n (fun y => c y v) x m =
    iteratedFDeriv ğ•œ n (fun y => c y) x m v := by
  symm
  induction n generalizing Î± G with
  | zero => simp
  | succ n h_ind =>
    simp [iteratedFDeriv_succ_apply_right]
    -- generalize m (Fin.last n) = m_last  -- For readability.
    -- generalize Fin.init m = m_init
    have hc_diff : Differentiable ğ•œ (fun y => c y) := hc.differentiable (by norm_num)
    have hc_fderiv : ContDiff ğ•œ n (fun y => fderiv ğ•œ c y) := hc.fderiv_right (by norm_cast)
    rw [h_ind hc_fderiv]
    rw [h_ind (hc_fderiv.clm_apply contDiff_const)]
    rw [h_ind]
    swap
    . conv => arg 3; intro y; rw [fderiv_clm_apply (hc_diff y) (differentiableAt_const v)]
      simp
      refine ContDiff.clm_apply ?_ contDiff_const
      change ContDiff ğ•œ n fun y => (ContinuousLinearMap.flipâ‚—áµ¢ ğ•œ Î² Î± G).toContinuousLinearEquiv.toContinuousLinearMap (fderiv ğ•œ c y)
      exact ContDiff.continuousLinearMap_comp _ hc_fderiv
    congr
    funext z
    rw [fderiv_clm_apply (hc_diff z) (differentiableAt_const v)]
    simp

lemma iteratedFDeriv_fderiv_apply {n : â„•} {f : Î± â†’ G} {x : Î±} {m : Fin n â†’ Î±} {v : Î±} (hf : ContDiff ğ•œ n.succ f) :
    iteratedFDeriv ğ•œ n (fun y => fderiv ğ•œ f y v) x m =
    iteratedFDeriv ğ•œ n (fun y => fderiv ğ•œ f y) x m v := by
  rw [contDiff_succ_iff_fderiv] at hf
  rw [iteratedFDeriv_clm_apply_const hf.right]

-- How to use `LinearIsometryEquiv` for multilinear maps.

example {y : Î±} {c : Î± â†’ Î±[Ã—0]â†’L[ğ•œ] G} : (c y) 0 = (continuousMultilinearCurryFin0 ğ•œ Î± G) (c y) := rfl

example {y : Î±} {c : Î± â†’ Î±[Ã—(n + 1)]â†’L[ğ•œ] G} :
    ContinuousMultilinearMap.curryRight (c y) =
    (continuousMultilinearCurryRightEquiv' ğ•œ n Î± G).symm (c y) := rfl

example {y : Î±} {c : Î± â†’ Î±[Ã—(n + 1)]â†’L[ğ•œ] G} :
    ContinuousMultilinearMap.curryLeft (c y) =
    (continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) => Î±) G).symm (c y) := rfl

/-- The FrÃ©chet derivative of the application of a `ContinuousMultilinearMap`.

TODO: Add version for multilinear map with pi type `(i : Fin n) â†’ Î± i`.
-/
theorem hasFDerivAt_continuousMultilinearMap_apply_const' {n : â„•} {c : Î² â†’ Î±[Ã—n]â†’L[ğ•œ] G} {v : Fin n â†’ Î±} {x : Î²} {c' : Î² â†’L[ğ•œ] Î±[Ã—n]â†’L[ğ•œ] G}
    (hc : HasFDerivAt c c' x) :
    HasFDerivAt (fun y : Î² => (c y) v) (ContinuousLinearMap.flipMultilinear c' v) x := by
  induction n generalizing G with
  | zero =>
    rw [Matrix.empty_eq v, â† Matrix.empty_eq 0]  -- TODO: More idiomatic way?
    change HasFDerivAt (continuousMultilinearCurryFin0 ğ•œ Î± G âˆ˜ c) _ x
    -- have he : HasFDerivAt (continuousMultilinearCurryFin0 ğ•œ Î± G) _ (c x) := LinearIsometryEquiv.hasFDerivAt _
    refine HasFDerivAt.congr_fderiv (HasFDerivAt.comp x (continuousMultilinearCurryFin0 ğ•œ Î± G).hasFDerivAt hc) ?_
    ext a
    simp
  | succ n h_ind =>
    conv => arg 1; intro y; rw [â† Fin.snoc_init_self v, â† ContinuousMultilinearMap.curryRight_apply]
    -- Need to find derivative of
    -- `(c y) v = ContinuousMultilinearMap.curryRight (c y) (Fin.init v) (v (Fin.last n))`
    -- where
    -- `c y : Î±[Ã—(n + 1)]â†’L[ğ•œ] G`
    -- `ContinuousMultilinearMap.curryRight (c y) : Î± â†’L[ğ•œ] Î±[Ã—n]â†’L[ğ•œ] G`  -- use `clm_apply`
    -- `ContinuousMultilinearMap.curryRight (c y) _ : Î±[Ã—n]â†’L[ğ•œ] G`  -- use induction

    -- Find derivative of `fun y => ContinuousMultilinearMap.curryRight (c y)`.
    have he : HasFDerivAt (continuousMultilinearCurryRightEquiv' ğ•œ n Î± G).symm _ (c x) := LinearIsometryEquiv.hasFDerivAt _
    have := HasFDerivAt.comp x he hc
    conv at this => arg 1; intro y; change ContinuousMultilinearMap.curryRight (c y)
    -- Find derivative of `fun y => ContinuousMultilinearMap.curryRight (c y) (Fin.init v)`.
    replace : HasFDerivAt (fun y => (ContinuousMultilinearMap.curryRight (c y)) (Fin.init v)) _ x := h_ind this
    -- Find derivative of `fun y => ContinuousMultilinearMap.curryRight (c y) (Fin.init v) (v (Fin.last n))`.
    refine HasFDerivAt.congr_fderiv (HasFDerivAt.clm_apply this (hasFDerivAt_const _ x)) ?_
    clear this
    ext b
    simp

/-- The application of a `ContinuousMultilinearMap` can be moved outside `fderiv`. -/
theorem fderiv_continuousMultilinearMap_apply_const {n : â„•} {c : Î² â†’ Î±[Ã—n]â†’L[ğ•œ] G} {v : Fin n â†’ Î±} {x m : Î²} (hc : DifferentiableAt ğ•œ c x) :
    fderiv ğ•œ (fun y => c y v) x m =
    fderiv ğ•œ (fun y => c y) x m v := by
  rw [(hasFDerivAt_continuousMultilinearMap_apply_const' hc.hasFDerivAt).fderiv]
  simp

lemma fderiv_iteratedFDeriv_apply {n : â„•} {f : Î± â†’ G} {m : Fin n â†’ Î±} {x : Î±} {v : Î±} (hf : ContDiff ğ•œ n.succ f) :
    fderiv ğ•œ (fun y => iteratedFDeriv ğ•œ n f y m) x v =
    fderiv ğ•œ (fun y => iteratedFDeriv ğ•œ n f y) x v m :=
  fderiv_continuousMultilinearMap_apply_const (hf.differentiable_iteratedFDeriv (by norm_cast; simp) x)

-- TODO: `iteratedFDeriv_continuousMultilinearMap_apply_const`

end Induction
