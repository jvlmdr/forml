-- https://github.com/leanprover-community/mathlib4/pull/10022

import Mathlib.Algebra.BigOperators.Basic
import Mathlib.Analysis.Calculus.ContDiff.Bounds
import Mathlib.Data.Nat.Choose.Multinomial

open scoped BigOperators


namespace Finset  -- Mathlib/Algebra/BigOperators/Basic.lean

variable {Î¹ : Type*} {Î² : Type u} {Î± : Type v}

section
variable [CommMonoid Î²]

@[to_additive]
theorem prod_update_one_of_mem [DecidableEq Î±] {s : Finset Î±} {i : Î±} (hi : i âˆˆ s) (f : Î± â†’ Î²) :
    âˆ j in s, Function.update f i 1 j = âˆ j in s \ singleton i, f j := by
  simp [prod_update_of_mem hi]

end

section
variable [Fintype Î¹] [CommMonoid Î±]

@[to_additive]
theorem prod_erase_attach [DecidableEq Î¹] {s : Finset Î¹} (f : Î¹ â†’ Î±) (i : â†‘s) :
    âˆ j in s.attach.erase i, f â†‘j = âˆ j in s.erase â†‘i, f j := by
  simp only [erase_eq]
  rw [â† prod_update_one_of_mem i.prop, â† prod_update_one_of_mem (mem_attach s i)]
  rw [â† prod_coe_sort s]
  refine prod_congr rfl ?_
  intro j _
  simp only [â† Function.comp_apply (g := Subtype.val) (x := j)]
  rw [Function.update_comp_eq_of_injective _ Subtype.val_injective]
  rfl

end

end Finset


section  -- Mathlib/Analysis/Calculus/ContDiff/Basic.lean

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {D : Type uD} [NormedAddCommGroup D]
  [NormedSpace ğ•œ D] {E : Type uE} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {F : Type uF}
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type uG} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {X : Type*} [NormedAddCommGroup X] [NormedSpace ğ•œ X] {s sâ‚ t u : Set E} {f fâ‚ : E â†’ F}
  {g : F â†’ G} {x xâ‚€ : E} {c : F} {b : E Ã— F â†’ G} {m n : â„•âˆ} {p : E â†’ FormalMultilinearSeries ğ•œ E F}

@[simp]
theorem iteratedFDerivWithin_zero_fun (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {i : â„•} :
    iteratedFDerivWithin ğ•œ i (fun _ : E â†¦ (0 : F)) s x = 0 := by
  induction i generalizing x with
  | zero => ext; simp
  | succ i IH =>
    ext m
    rw [iteratedFDerivWithin_succ_apply_left, fderivWithin_congr (fun _ â†¦ IH) (IH hx)]
    rw [fderivWithin_const_apply _ (hs x hx)]
    rfl

theorem iteratedFDerivWithin_succ_const (n : â„•) (c : F) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    iteratedFDerivWithin ğ•œ (n + 1) (fun _ : E â†¦ c) s x = 0 := by
  ext m
  rw [iteratedFDerivWithin_succ_apply_right hs hx]
  rw [iteratedFDerivWithin_congr (fun y hy â†¦ fderivWithin_const_apply c (hs y hy)) hx]
  rw [iteratedFDerivWithin_zero_fun hs hx]
  simp [ContinuousMultilinearMap.zero_apply (R := ğ•œ)]

theorem iteratedFDerivWithin_const_of_ne {n : â„•} (hn : n â‰  0) (c : F)
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    iteratedFDerivWithin ğ•œ n (fun _ : E â†¦ c) s x = 0 := by
  cases n with
  | zero => contradiction
  | succ n => exact iteratedFDerivWithin_succ_const _ _ hs hx

section

variable {Î¹ : Type*} [DecidableEq Î¹]

theorem iteratedFDerivWithin_sum_apply {f : Î¹ â†’ E â†’ F} {u : Finset Î¹} {i : â„•} {x : E}
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (h : âˆ€ j âˆˆ u, ContDiffOn ğ•œ i (f j) s) :
    iteratedFDerivWithin ğ•œ i (âˆ‘ j in u, f j Â·) s x =
      âˆ‘ j in u, iteratedFDerivWithin ğ•œ i (f j) s x := by
  induction u using Finset.induction with
  | empty => ext; simp [hs, hx]
  | @insert a u ha IH =>
    simp only [Finset.mem_insert] at h
    simp only [Finset.mem_insert, forall_eq_or_imp] at h
    simp only [Finset.sum_insert ha]
    rw [iteratedFDerivWithin_add_apply' h.1 (ContDiffOn.sum h.2) hs hx, IH h.2]

theorem iteratedFDeriv_sum {f : Î¹ â†’ E â†’ F} {u : Finset Î¹} {i : â„•}
    (h : âˆ€ j âˆˆ u, ContDiff ğ•œ i (f j)) :
    iteratedFDeriv ğ•œ i (âˆ‘ j in u, f j Â·) = âˆ‘ j in u, iteratedFDeriv ğ•œ i (f j) := by
  simp only [â† iteratedFDerivWithin_univ]
  funext x
  rw [Finset.sum_apply]
  exact iteratedFDerivWithin_sum_apply uniqueDiffOn_univ trivial fun j hj â†¦ (h j hj).contDiffOn

end


section  -- Mathlib/Analysis/Calculus/FDeriv/Mul.lean

open ContinuousLinearMap

section Prod

open BigOperators

/-! ### Derivative of a finite product of functions -/

variable {Î¹ : Type*} [DecidableEq Î¹] {ğ”¸' : Type*} [NormedCommRing ğ”¸'] [NormedAlgebra ğ•œ ğ”¸']
  {u : Finset Î¹} {f : Î¹ â†’ E â†’ ğ”¸'} {f' : Î¹ â†’ E â†’L[ğ•œ] ğ”¸'}

section Fintype

variable [Fintype Î¹]

theorem hasStrictFDerivAt_finset_prod_univ {x : Î¹ â†’ ğ”¸'} :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (âˆ i, Â· i) (âˆ‘ i, (âˆ j in Finset.univ.erase i, x j) â€¢ proj i) x := by
  generalize (Finset.univ : Finset Î¹) = u
  induction u using Finset.induction with
  | empty => simp [hasStrictFDerivAt_const]
  | @insert i u hi IH =>
    simp only [Finset.prod_insert hi]
    refine ((proj i).hasStrictFDerivAt.mul' IH).congr_fderiv ?_
    simp only [Finset.sum_insert hi, Finset.erase_insert hi]
    rw [add_comm]
    refine congrArgâ‚‚ _ ?_ ?_
    Â· ext m
      simp only [smulRight_apply (R := ğ•œ), smul_apply, smul_eq_mul]
      exact mul_comm _ _
    Â· ext m
      simp only [smul_apply (Râ‚ := ğ•œ), sum_apply (Râ‚ := ğ•œ), Finset.smul_sum, smul_smul, proj_apply]
      refine Finset.sum_congr rfl ?_
      intro k hk
      rw [Finset.erase_insert_of_ne fun hik â†¦ hi <| by simpa [hik]]
      rw [Finset.prod_insert <| by simp [hi]]

theorem hasFDerivAt_finset_prod_univ {x : Î¹ â†’ ğ”¸'} :
    HasFDerivAt (ğ•œ := ğ•œ) (âˆ i, Â· i) (âˆ‘ i, (âˆ j in Finset.univ.erase i, x j) â€¢ proj i) x :=
  hasStrictFDerivAt_finset_prod_univ.hasFDerivAt

theorem ContinuousMultilinearMap.hasStrictFDerivAt_mkPiAlgebra {x : Î¹ â†’ ğ”¸'} :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (ContinuousMultilinearMap.mkPiAlgebra ğ•œ Î¹ ğ”¸')
      (âˆ‘ i, (âˆ j in Finset.univ.erase i, x j) â€¢ proj i) x :=
  hasStrictFDerivAt_finset_prod_univ

theorem ContinuousMultilinearMap.hasFDerivAt_mkPiAlgebra {x : Î¹ â†’ ğ”¸'} :
    HasFDerivAt (ğ•œ := ğ•œ) (ContinuousMultilinearMap.mkPiAlgebra ğ•œ Î¹ ğ”¸')
      (âˆ‘ i, (âˆ j in Finset.univ.erase i, x j) â€¢ proj i) x :=
  hasStrictFDerivAt_mkPiAlgebra.hasFDerivAt

theorem HasFDerivAt.finset_prod_univ {x : E} (hf : âˆ€ i, HasFDerivAt (f i) (f' i) x) :
    HasFDerivAt (âˆ i, f i Â·) (âˆ‘ i, (âˆ j in Finset.univ.erase i, f j x) â€¢ f' i) x := by
  refine (hasFDerivAt_finset_prod_univ.comp x <| hasFDerivAt_pi.mpr hf).congr_fderiv ?_
  ext m
  simp [comp_apply (Râ‚ := ğ•œ), sum_apply (Râ‚ := ğ•œ), smul_apply]

theorem HasStrictFDerivAt.finset_prod_univ {x : E} (hf : âˆ€ i, HasStrictFDerivAt (f i) (f' i) x) :
    HasStrictFDerivAt (âˆ i, f i Â·) (âˆ‘ i, (âˆ j in Finset.univ.erase i, f j x) â€¢ f' i) x := by
  refine (hasStrictFDerivAt_finset_prod_univ.comp x <| hasStrictFDerivAt_pi.mpr hf).congr_fderiv ?_
  ext m
  simp [comp_apply (Râ‚ := ğ•œ), sum_apply (Râ‚ := ğ•œ), smul_apply]

theorem HasFDerivWithinAt.finset_prod_univ {x : E} (hf : âˆ€ i, HasFDerivWithinAt (f i) (f' i) s x) :
    HasFDerivWithinAt (âˆ i, f i Â·) (âˆ‘ i, (âˆ j in Finset.univ.erase i, f j x) â€¢ f' i) s x := by
  refine HasFDerivWithinAt.congr_fderiv
    (hasFDerivAt_finset_prod_univ.comp_hasFDerivWithinAt x <| hasFDerivWithinAt_pi.mpr hf) ?_
  ext m
  simp [comp_apply (Râ‚ := ğ•œ), sum_apply (Râ‚ := ğ•œ), smul_apply]

end Fintype

section Comp

theorem HasFDerivAt.finset_prod {x : E} (hf : âˆ€ i âˆˆ u, HasFDerivAt (f i) (f' i) x) :
    HasFDerivAt (âˆ i in u, f i Â·) (âˆ‘ i in u, (âˆ j in u.erase i, (f j x)) â€¢ f' i) x := by
  simp only [â† Finset.prod_coe_sort u]
  refine (finset_prod_univ fun i : u â†¦ hf i i.prop).congr_fderiv ?_
  rw [â† Finset.sum_attach u, Finset.univ_eq_attach]
  exact Finset.sum_congr rfl fun i _ â†¦ congrArgâ‚‚ _ (Finset.prod_erase_attach (f Â· x) i) rfl

theorem HasFDerivWithinAt.finset_prod {x : E} (hf : âˆ€ i âˆˆ u, HasFDerivWithinAt (f i) (f' i) s x) :
    HasFDerivWithinAt (âˆ i in u, f i Â·) (âˆ‘ i in u, (âˆ j in u.erase i, (f j x)) â€¢ f' i) s x := by
  simp only [â† Finset.prod_coe_sort u]
  refine (finset_prod_univ fun i : u â†¦ hf i i.prop).congr_fderiv ?_
  rw [â† Finset.sum_attach u, Finset.univ_eq_attach]
  exact Finset.sum_congr rfl fun i _ â†¦ congrArgâ‚‚ _ (Finset.prod_erase_attach (f Â· x) i) rfl

theorem HasStrictFDerivAt.finset_prod {x : E} (hf : âˆ€ i âˆˆ u, HasStrictFDerivAt (f i) (f' i) x) :
    HasStrictFDerivAt (âˆ i in u, f i Â·) (âˆ‘ i in u, (âˆ j in u.erase i, (f j x)) â€¢ f' i) x := by
  simp only [â† Finset.prod_coe_sort u]
  refine (finset_prod_univ fun i : u â†¦ hf i i.prop).congr_fderiv ?_
  rw [â† Finset.sum_attach u, Finset.univ_eq_attach]
  exact Finset.sum_congr rfl fun i _ â†¦ congrArgâ‚‚ _ (Finset.prod_erase_attach (f Â· x) i) rfl

theorem fderiv_finset_prod {x : E} (hf : âˆ€ i âˆˆ u, DifferentiableAt ğ•œ (f i) x) :
    fderiv ğ•œ (âˆ i in u, f i Â·) x = âˆ‘ i in u, (âˆ j in u.erase i, (f j x)) â€¢ fderiv ğ•œ (f i) x :=
  (HasFDerivAt.finset_prod (fun i hi â†¦ (hf i hi).hasFDerivAt)).fderiv

theorem fderivWithin_finset_prod {x : E} (hxs : UniqueDiffWithinAt ğ•œ s x)
    (hf : âˆ€ i âˆˆ u, DifferentiableWithinAt ğ•œ (f i) s x) :
    fderivWithin ğ•œ (âˆ i in u, f i Â·) s x =
      âˆ‘ i in u, (âˆ j in u.erase i, (f j x)) â€¢ fderivWithin ğ•œ (f i) s x :=
  (HasFDerivWithinAt.finset_prod (fun i hi â†¦ (hf i hi).hasFDerivWithinAt)).fderivWithin hxs

end Comp

end Prod

end


section  -- Mathlib/Analysis/Calculus/Deriv/Mul.lean

variable {x : ğ•œ}
variable {s t : Set ğ•œ}

section Prod

variable {Î¹ : Type*} [DecidableEq Î¹] {ğ”¸' : Type*} [NormedCommRing ğ”¸'] [NormedAlgebra ğ•œ ğ”¸']
  {u : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ”¸'} {f' : Î¹ â†’ ğ”¸'}

theorem HasDerivAt.finset_prod (hf : âˆ€ i âˆˆ u, HasDerivAt (f i) (f' i) x) :
    HasDerivAt (âˆ i in u, f i Â·) (âˆ‘ i in u, (âˆ j in u.erase i, f j x) â€¢ f' i) x := by
  simpa [ContinuousLinearMap.sum_apply, ContinuousLinearMap.smul_apply] using
    (HasFDerivAt.finset_prod (fun i hi â†¦ (hf i hi).hasFDerivAt)).hasDerivAt

theorem HasDerivWithinAt.finset_prod (hf : âˆ€ i âˆˆ u, HasDerivWithinAt (f i) (f' i) s x) :
    HasDerivWithinAt (âˆ i in u, f i Â·) (âˆ‘ i in u, (âˆ j in u.erase i, f j x) â€¢ f' i) s x := by
  simpa [ContinuousLinearMap.sum_apply, ContinuousLinearMap.smul_apply] using
    (HasFDerivWithinAt.finset_prod (fun i hi â†¦ (hf i hi).hasFDerivWithinAt)).hasDerivWithinAt

theorem HasStrictDerivAt.finset_prod (hf : âˆ€ i âˆˆ u, HasStrictDerivAt (f i) (f' i) x) :
    HasStrictDerivAt (âˆ i in u, f i Â·) (âˆ‘ i in u, (âˆ j in u.erase i, f j x) â€¢ f' i) x := by
  simpa [ContinuousLinearMap.sum_apply, ContinuousLinearMap.smul_apply] using
    (HasStrictFDerivAt.finset_prod (fun i hi â†¦ (hf i hi).hasStrictFDerivAt)).hasStrictDerivAt

theorem deriv_finset_prod (hf : âˆ€ i âˆˆ u, DifferentiableAt ğ•œ (f i) x) :
    deriv (âˆ i in u, f i Â·) x = âˆ‘ i in u, (âˆ j in u.erase i, f j x) â€¢ deriv (f i) x :=
  (HasDerivAt.finset_prod fun i hi â†¦ (hf i hi).hasDerivAt).deriv

theorem derivWithin_finset_prod (hxs : UniqueDiffWithinAt ğ•œ s x)
    (hf : âˆ€ i âˆˆ u, DifferentiableWithinAt ğ•œ (f i) s x) :
    derivWithin (âˆ i in u, f i Â·) s x =
      âˆ‘ i in u, (âˆ j in u.erase i, f j x) â€¢ derivWithin (f i) s x :=
  (HasDerivWithinAt.finset_prod fun i hi â†¦ (hf i hi).hasDerivWithinAt).derivWithin hxs

end Prod

end


namespace Sym  -- Mathlib/Data/Sym/Basic.lean

open Multiset

variable {Î± Î² : Type*} {n n' m : â„•} {s : Sym Î± n} {a b : Î±}

@[simp]
theorem card_coe : Multiset.card (s : Multiset Î±) = n := s.prop

theorem count_coe_fill_self_of_not_mem [DecidableEq Î±] {a : Î±} {i : Fin (n + 1)} {s : Sym Î± (n - i)}
    (hx : a âˆ‰ s) :
    count a (fill a i s : Multiset Î±) = i := by
  simp [coe_fill, coe_replicate, hx]

theorem count_coe_fill_of_ne [DecidableEq Î±] {a x : Î±} {i : Fin (n + 1)} {s : Sym Î± (n - i)}
    (hx : x â‰  a) :
    count x (fill a i s : Multiset Î±) = count x s := by
  suffices : x âˆ‰ Multiset.replicate i a
  Â· simp [coe_fill, coe_replicate, this]
  simp [Multiset.mem_replicate, hx]

end Sym


section  -- Mathlib/Data/Nat/Choose/Multinomial.lean

namespace Multiset

variable {Î± : Type*}

@[simp]
theorem multinomial_zero [DecidableEq Î±] : multinomial (0 : Multiset Î±) = 1 := rfl

end Multiset

namespace Sym

variable {n : â„•} {Î± : Type*} [DecidableEq Î±]

theorem multinomial_coe_fill_of_not_mem {m : Fin (n + 1)} {s : Sym Î± (n - m)} {x : Î±} (hx : x âˆ‰ s) :
    (fill x m s : Multiset Î±).multinomial = n.choose m * (s : Multiset Î±).multinomial := by
  rw [Multiset.multinomial_filter_ne x]
  rw [â† mem_coe] at hx
  refine congrArgâ‚‚ _ ?_ ?_
  Â· rw [card_coe, count_coe_fill_self_of_not_mem hx]
  Â· refine congrArg _ ?_
    rw [coe_fill, coe_replicate, Multiset.filter_add]
    rw [Multiset.filter_eq_self.mpr]
    Â· rw [add_right_eq_self]
      rw [Multiset.filter_eq_nil]
      exact fun j hj â†¦ by simp [Multiset.mem_replicate.mp hj]
    Â· exact fun j hj h â†¦ hx <| by simpa [h] using hj

end Sym

end


section  -- Mathlib/Analysis/Calculus/ContDiff/Bounds.lean

open Function

variable {A' : Type*} [NormedCommRing A'] [NormedAlgebra ğ•œ A']
variable {Î¹ : Type*} [DecidableEq Î¹]

theorem norm_iteratedFDerivWithin_prod_le [NormOneClass A'] {u : Finset Î¹} {f : Î¹ â†’ E â†’ A'}
    {N : â„•âˆ} (hf : âˆ€ i âˆˆ u, ContDiffOn ğ•œ N (f i) s) (hs : UniqueDiffOn ğ•œ s) {x : E} (hx : x âˆˆ s)
    {n : â„•} (hn : (n : â„•âˆ) â‰¤ N) :
    â€–iteratedFDerivWithin ğ•œ n (âˆ j in u, f j Â·) s xâ€– â‰¤
      âˆ‘ p in u.sym n, (p : Multiset Î¹).multinomial *
        âˆ j in u, â€–iteratedFDerivWithin ğ•œ ((p : Multiset Î¹).count j) (f j) s xâ€– := by
  induction u using Finset.induction generalizing n with
  | empty =>
    cases n with
    | zero => simp [Sym.eq_nil_of_card_zero]
    | succ n => simp [iteratedFDerivWithin_succ_const _ _ hs hx]
  | @insert i u hi IH =>
    conv => lhs; simp only [Finset.prod_insert hi]
    simp only [Finset.mem_insert, forall_eq_or_imp] at hf
    refine le_trans (norm_iteratedFDerivWithin_mul_le hf.1 (contDiffOn_prod hf.2) hs hx hn) ?_
    rw [â† Finset.sum_coe_sort (Finset.sym _ _)]
    rw [Finset.sum_equiv (Finset.symInsertEquiv hi) (t := Finset.univ)
      (g := (fun v â†¦ v.multinomial *
          âˆ j in insert i u, â€–iteratedFDerivWithin ğ•œ (v.count j) (f j) s xâ€–) âˆ˜
        Sym.toMultiset âˆ˜ Subtype.val âˆ˜ (Finset.symInsertEquiv hi).symm)
      (by simp) (by simp only [â† comp_apply (g := Finset.symInsertEquiv hi), comp.assoc]; simp)]
    rw [â† Finset.univ_sigma_univ, Finset.sum_sigma, Finset.sum_range]
    simp only [comp_apply, Finset.symInsertEquiv_symm_apply_coe]
    refine Finset.sum_le_sum ?_
    simp only [Finset.mem_univ, forall_true_left]
    intro m
    specialize IH hf.2 (n := n - m) (le_trans (WithTop.coe_le_coe.mpr (n.sub_le m)) hn)
    refine le_trans (mul_le_mul_of_nonneg_left IH (by simp [mul_nonneg])) ?_
    rw [Finset.mul_sum, â† Finset.sum_coe_sort]
    refine Finset.sum_le_sum ?_
    simp only [Finset.mem_univ, forall_true_left, Subtype.forall, Finset.mem_sym_iff]
    intro p hp
    refine le_of_eq ?_
    rw [Finset.prod_insert hi]
    have hip : i âˆ‰ p := fun h â†¦ hi <| hp i h
    rw [Sym.count_coe_fill_self_of_not_mem hip, Sym.multinomial_coe_fill_of_not_mem hip]
    suffices : âˆ j in u, â€–iteratedFDerivWithin ğ•œ (Multiset.count j p) (f j) s xâ€– =
        âˆ j in u, â€–iteratedFDerivWithin ğ•œ (Multiset.count j (Sym.fill i m p)) (f j) s xâ€–
    Â· rw [this, Nat.cast_mul]
      ring
    refine Finset.prod_congr rfl ?_
    intro j hj
    have hji : j â‰  i := fun h â†¦ hi <| by simpa [h] using hj
    rw [Sym.count_coe_fill_of_ne hji]

theorem norm_iteratedFDeriv_prod_le [NormOneClass A'] {u : Finset Î¹} {f : Î¹ â†’ E â†’ A'}
    {N : â„•âˆ} (hf : âˆ€ i âˆˆ u, ContDiff ğ•œ N (f i)) {x : E} {n : â„•} (hn : (n : â„•âˆ) â‰¤ N) :
    â€–iteratedFDeriv ğ•œ n (âˆ j in u, f j Â·) xâ€– â‰¤
      âˆ‘ p in u.sym n, (p : Multiset Î¹).multinomial *
        âˆ j in u, â€–iteratedFDeriv ğ•œ ((p : Multiset Î¹).count j) (f j) xâ€– := by
  simpa [iteratedFDerivWithin_univ] using
    norm_iteratedFDerivWithin_prod_le (fun i hi â†¦ (hf i hi).contDiffOn) uniqueDiffOn_univ trivial hn

end
