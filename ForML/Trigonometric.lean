import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.Analysis.SpecialFunctions.ExpDeriv
import Mathlib.Analysis.SpecialFunctions.Exponential
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv

import ForML.HasTemperateGrowth
import ForML.Util

open Complex
open scoped BigOperators Real FourierTransform RealInnerProductSpace

section IteratedDeriv
-- Some useful lemmata for `iteratedDeriv` that avoid passing through `iteratedFDeriv`.
-- TODO: Is it overkill to declare these?
-- TODO: Typeclasses might not be perfect; some trial and error involved.

variable {ğ•œ ğ•œ' F : Type*}
variable [NontriviallyNormedField ğ•œ]
variable [NormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
variable [NormedAddCommGroup F] [NormedSpace ğ•œ F]

lemma iteratedDeriv_neg_apply {n : â„•} {f : ğ•œ â†’ F} :
    iteratedDeriv n (fun x => -f x) x = -iteratedDeriv n f x := by
  change iteratedDeriv n (-f) x = _
  simp only [iteratedDeriv_eq_iteratedFDeriv]
  simp [iteratedFDeriv_neg_apply]

lemma iteratedDeriv_const_mul_apply {n : â„•} (a : ğ•œ') {f : ğ•œ â†’ ğ•œ'} (hf : ContDiff ğ•œ n f) {x : ğ•œ} :
    iteratedDeriv n (fun x => a * f x) x = a * iteratedDeriv n f x := by
  change iteratedDeriv n (a â€¢ f) x = _
  simp only [iteratedDeriv_eq_iteratedFDeriv]
  simp [iteratedFDeriv_const_smul_apply hf]

lemma iteratedDeriv_mul_const_apply {n : â„•} (a : ğ•œ') {f : ğ•œ â†’ ğ•œ'} (hf : ContDiff ğ•œ n f) {x : ğ•œ} :
    iteratedDeriv n (fun x => f x * a) x = iteratedDeriv n f x * a := by
  simp_rw [mul_comm _ a]
  exact iteratedDeriv_const_mul_apply a hf

end IteratedDeriv


section Complex

variable {ğ•œ E : Type*}
variable [NontriviallyNormedField ğ•œ] [NormedAlgebra ğ•œ â„]
variable [NormedAddCommGroup E] [NormedSpace â„ E]

lemma Complex.contDiff_exp_real_smul_I {n : â„•âˆ} : ContDiff â„ n fun x : â„ => exp (x â€¢ I) :=
  (contDiff_id.smul contDiff_const).cexp

lemma Complex.contDiff_exp_neg_real_smul_I {n : â„•âˆ} : ContDiff â„ n fun x : â„ => exp (-(x â€¢ I)) :=
  (contDiff_id.smul contDiff_const).neg.cexp

lemma ContDiff.cexp_real_smul_I {n : â„•âˆ} {f : E â†’ â„} (hf : ContDiff â„ n f) :
    ContDiff â„ n fun x => cexp (f x â€¢ Complex.I) :=
  Complex.contDiff_exp_real_smul_I.comp hf

lemma ContDiff.realFourierChar {n : â„•âˆ} {f : E â†’ â„} (hf : ContDiff â„ n f) :
    ContDiff â„ n fun x => Real.fourierChar[f x] :=
  ((contDiff_const.mul hf).smul contDiff_const).cexp

lemma Real.contDiff_fourierChar {n : â„•âˆ} : ContDiff â„ n fun x : â„ => fourierChar[x] :=
  ((contDiff_const.mul contDiff_id).smul contDiff_const).cexp

lemma Real.differentiable_fourierChar : Differentiable â„ fun x : â„ => fourierChar[x] :=
  contDiff_fourierChar.differentiable le_top

lemma Real.differentiableAt_fourierChar {x : â„} : DifferentiableAt â„ (fun x : â„ => fourierChar[x]) x :=
  differentiable_fourierChar.differentiableAt

lemma Complex.hasDerivAt_exp_real_smul_I {x : â„} :
    HasDerivAt (fun x : â„ => exp (x â€¢ I)) (I * exp (x â€¢ I)) x := by
  -- rw [Complex.exp_eq_exp_â„‚]
  -- refine hasDerivAt_exp_smul_const (ğ•‚ := â„) (ğ”¸ := â„‚) ?_ x  -- Gives `NormedSpace.exp â„`
  rw [mul_comm I]
  refine HasDerivAt.cexp ?_
  suffices : HasDerivAt (fun x => x â€¢ I) ((1 : â„) â€¢ I) x
  . simpa using this
  exact HasDerivAt.smul_const (hasDerivAt_id' x) I

lemma Complex.deriv_exp_real_smul_I {x : â„} :
    deriv (fun x : â„ => exp (x â€¢ I)) x = I * exp (x â€¢ I) :=
  hasDerivAt_exp_real_smul_I.deriv

lemma Complex.iteratedDeriv_exp_real_smul_I {n : â„•} {x : â„} :
    iteratedDeriv n (fun x : â„ => exp (x â€¢ I)) x = HPow.hPow I n * exp (x â€¢ I) := by
  induction n with
  | zero => simp
  | succ n hi =>
    rw [iteratedDeriv_succ', pow_succ]
    conv => lhs; arg 2; intro x; rw [deriv_exp_real_smul_I]
    rw [iteratedDeriv_const_mul_apply I contDiff_exp_real_smul_I]
    rw [hi, mul_assoc]

lemma Complex.hasTemperateGrowth_exp_real_smul_I :
    Function.HasTemperateGrowth fun x : â„ => exp (x â€¢ I) := by
  refine âŸ¨contDiff_exp_real_smul_I, ?_âŸ©
  intro n
  refine âŸ¨n, 1, ?_âŸ©
  intro x
  rw [norm_iteratedFDeriv_eq_norm_iteratedDeriv]
  rw [iteratedDeriv_exp_real_smul_I]
  simp
  exact one_le_pow_of_one_le (by simp) n

/-- `d(exp (2 Ï€ I x)) = (2 Ï€ I) exp (2 Ï€ I x) dx` -/
lemma Real.fderiv_fourierChar_apply {x dx : â„} :
    fderiv â„ (fun x : â„ => Real.fourierChar[x]) x dx =
    (2 * Ï€ * dx) â€¢ (Complex.I * Real.fourierChar[x]) := by
  simp [Real.fourierChar]
  norm_cast
  -- TODO: Re-order to avoid O(n^2) re-proving?
  rw [fderiv_cexp_real]
  swap
  . simp_rw [â† Complex.real_smul]
    exact (differentiableAt_id.const_mul _).smul_const _
  simp
  rw [ContinuousLinearMap.smul_apply]
  simp
  norm_cast
  simp_rw [â† Complex.real_smul]
  rw [fderiv_smul_const]
  swap
  . exact DifferentiableAt.const_mul differentiableAt_id _
  rw [ContinuousLinearMap.smulRight_apply]
  simp
  rw [fderiv_const_mul differentiableAt_id']
  simp
  ring_nf

lemma Real.fderiv_fourierChar {x : â„} :
    fderiv â„ (fun x => Real.fourierChar[(x : â„)]) x =
    (2 * Ï€ * Complex.I * Real.fourierChar[x]) â€¢ Complex.ofRealCLM := by
  refine ContinuousLinearMap.ext ?_
  intro dx
  rw [fderiv_fourierChar_apply]
  simp
  ring_nf

/--
The imaginary exponential of a real-valued `HasTemperateGrowth` function is a `HasTemperateGrowth` function.

TODO: Prove for more general `g : E â†’ â„‚` with `|(g x).re| â‰¤ 1`?
-/
lemma Function.HasTemperateGrowth.cexp_real_smul_I {f : E â†’ â„} (hf : Function.HasTemperateGrowth f) :
    Function.HasTemperateGrowth fun x => cexp (f x â€¢ Complex.I) :=
  Complex.hasTemperateGrowth_exp_real_smul_I.comp hf

end Complex


section RealFourier

variable {E : Type*}
variable [NormedAddCommGroup E] [NormedSpace â„ E]

lemma Real.hasTemperateGrowth_cos : Function.HasTemperateGrowth (fun x : â„ => cos x) := by
  simp_rw [cos, Complex.cos]
  refine ((Function.HasTemperateGrowth.add ?_ ?_).div_const 2).re
  . simp_rw [â† Complex.real_smul]
    exact Complex.hasTemperateGrowth_exp_real_smul_I
  norm_cast
  simp_rw [â† Complex.real_smul]
  exact Function.hasTemperateGrowth_neg.cexp_real_smul_I

lemma Real.hasTemperateGrowth_sin : Function.HasTemperateGrowth (fun x => sin x) := by
  simp_rw [sin, Complex.sin]
  refine (((Function.HasTemperateGrowth.sub ?_ ?_).mul_const Complex.I).div_const 2).re
  . norm_cast
    simp_rw [â† Complex.real_smul]
    exact Function.hasTemperateGrowth_neg.cexp_real_smul_I
  simp_rw [â† Complex.real_smul]
  exact Complex.hasTemperateGrowth_exp_real_smul_I

lemma Real.hasTemperateGrowth_fourierChar :
    Function.HasTemperateGrowth fun x : â„ => fourierChar[x] := by
  simp [fourierChar_apply]
  norm_cast
  simp_rw [â† Complex.real_smul]
  exact (Function.hasTemperateGrowth_const_mul (2 * Ï€)).cexp_real_smul_I

lemma Function.HasTemperateGrowth.cos {f : E â†’ â„} (hf : HasTemperateGrowth f) :
    HasTemperateGrowth fun x => Real.cos (f x) := Real.hasTemperateGrowth_cos.comp hf

lemma Function.HasTemperateGrowth.sin {f : E â†’ â„} (hf : HasTemperateGrowth f) :
    HasTemperateGrowth fun x => Real.sin (f x) := Real.hasTemperateGrowth_sin.comp hf

lemma Function.HasTemperateGrowth.realFourierChar {f : E â†’ â„} (hf : HasTemperateGrowth f) :
    HasTemperateGrowth fun x => Real.fourierChar[f x] := Real.hasTemperateGrowth_fourierChar.comp hf

-- TODO: Too trivial to declare? May be useful for Fourier transform.

lemma Real.hasTemperateGrowth_fourierChar_mul_const (w : â„) : Function.HasTemperateGrowth fun v : â„ => fourierChar[v * w] :=
  (Function.hasTemperateGrowth_mul_const w).realFourierChar

lemma Real.hasTemperateGrowth_fourierChar_neg_mul_const (w : â„) : Function.HasTemperateGrowth fun v : â„ => fourierChar[-(v * w)] :=
  (Function.hasTemperateGrowth_mul_const w).neg.realFourierChar

end RealFourier


section VectorFourier

variable {E F : Type*}
variable [NormedAddCommGroup E] [NormedSpace â„ E]
variable [NormedAddCommGroup F] [InnerProductSpace â„ F]

example {v w : F} : (innerSL â„ v) w = âŸªv, wâŸ« := rfl
example {v w : F} : ((innerSL â„).flip v) w = âŸªw, vâŸ« := rfl

lemma Function.HasTemperateGrowth.inner {f g : E â†’ F} (hf : HasTemperateGrowth f) (hg : HasTemperateGrowth g) :
    Function.HasTemperateGrowth fun x => âŸªf x, g xâŸ« :=
  bilin (innerSL â„) hf hg

lemma Function.HasTemperateGrowth.inner_const {f : E â†’ F} (hf : HasTemperateGrowth f) (w : F) :
    Function.HasTemperateGrowth fun x => âŸªf x, wâŸ« :=
  inner hf (hasTemperateGrowth_const w)

lemma Function.HasTemperateGrowth.const_inner (v : F) {f : E â†’ F} (hf : HasTemperateGrowth f) :
    Function.HasTemperateGrowth fun x => âŸªv, f xâŸ« :=
  inner (hasTemperateGrowth_const v) hf

-- TODO: Too trivial to declare? May be useful for Fourier transform.

lemma Real.hasTemperateGrowth_inner_const (w : F) :
    Function.HasTemperateGrowth fun v : F => âŸªv, wâŸ« :=
  Function.HasTemperateGrowth.inner Function.hasTemperateGrowth_id (Function.hasTemperateGrowth_const w)

lemma Real.hasTemperateGrowth_l2inner_const {Î¹ : Type*} [Fintype Î¹] (w : Î¹ â†’ â„) :
    Function.HasTemperateGrowth fun v : Î¹ â†’ â„ => âˆ‘ i : Î¹, v i * w i := by
  change Function.HasTemperateGrowth ((fun v : EuclideanSpace â„ Î¹ => inner v w) âˆ˜ â‡‘(EuclideanSpace.equiv Î¹ â„).symm.toContinuousLinearMap)
  exact (Real.hasTemperateGrowth_inner_const _).comp (Function.hasTemperateGrowth_clm _)

lemma Real.hasTemperateGrowth_realFourierChar_inner_const (w : F) :
    Function.HasTemperateGrowth fun v : F => fourierChar[âŸªv, wâŸ«] :=
  (hasTemperateGrowth_inner_const w).realFourierChar

lemma Real.hasTemperateGrowth_realFourierChar_neg_inner_const (w : F) :
    Function.HasTemperateGrowth fun v : F => fourierChar[-âŸªv, wâŸ«] :=
  (hasTemperateGrowth_inner_const w).neg.realFourierChar

end VectorFourier


section RealFourier  -- TODO: Integrate with above.

variable {E : Type*}
variable [TopologicalSpace E]

-- lemma Continuous.cexp_real_smul_I {f : E â†’ â„} (h : Continuous f) : Continuous (fun x => cexp (f x â€¢ I)) := by
--   exact (h.smul continuous_const).cexp

lemma Continuous.realFourierChar {f : E â†’ â„} (hf : Continuous f) :
    Continuous (fun x => Real.fourierChar[f x]) := by
  simp_rw [Real.fourierChar_apply]
  simp_rw [â† Complex.real_smul]
  exact ((continuous_const.mul hf).smul continuous_const).cexp

lemma deriv.cexp_real_smul_I {f : â„ â†’ â„} {x : â„} (hf : DifferentiableAt â„ f x) :
    deriv (fun x : â„ => Complex.exp (f x â€¢ I)) x = (deriv f x) * I * Complex.exp (f x â€¢ I) := by
  rw [deriv_cexp (hf.smul_const I)]
  rw [deriv_smul hf (differentiableAt_const I)]
  simp
  ring_nf

-- `HasDerivAt.comp` only allows type to change in inner function.
lemma HasDerivAt.cexp_real_smul_I {f : â„ â†’ â„} {f' : â„} {x : â„} (hf : HasDerivAt f f' x) :
    HasDerivAt (fun x : â„ => Complex.exp (f x â€¢ I)) (f' * I * Complex.exp (f x â€¢ I)) x := by
  suffices : HasDerivAt (fun x : â„ => Complex.exp (f x â€¢ I)) (f' â€¢ (I * Complex.exp (f x â€¢ I))) x
  . simpa [â† mul_assoc] using this
  exact HasDerivAt.comp_of_tower x Complex.hasDerivAt_exp_real_smul_I hf

lemma Real.deriv_fourierChar {x : â„} : deriv (fun x : â„ => Real.fourierChar[x]) x = 2 * Ï€ * I * Real.fourierChar[x] := by
  rw [â† fderiv_deriv]
  rw [Real.fderiv_fourierChar_apply]
  simp
  ring_nf

lemma Real.hasDerivAt_fourierChar {x : â„} : HasDerivAt (fun x : â„ => Real.fourierChar[x]) (2 * Ï€ * I * Real.fourierChar[x]) x := by
  rw [â† deriv_fourierChar]
  exact Real.differentiable_fourierChar.differentiableAt.hasDerivAt

end RealFourier
