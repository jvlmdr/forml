import Mathlib.Algebra.BigOperators.Basic
import Mathlib.Data.Nat.Choose.Multinomial

import ForML.Multichoose

open scoped BigOperators

attribute [simp] Fin.succ_ne_zero


namespace Finset

variable {Î¹ Îº : Type*}

-- TODO: Is this the most natural choice? Better to use `MapsTo` and... inverse?
-- TODO: Add product and sum together?
/-- Convenient form of `Finset.sum_nbij` using composition with the inverse. -/
lemma sum_bijOn_comp_leftInv {E : Type*} [AddCommMonoid E]
    {s : Finset Î¹} {t : Finset Îº} {f : Î¹ â†’ E} {e : Î¹ â†’ Îº} {e' : Îº â†’ Î¹}
    (he : Set.BijOn e s t) (he' : Set.LeftInvOn e' e s) :
    âˆ‘ i in s, f i = âˆ‘ i in t, f (e' i) :=
  sum_nbij (i := e) (fun _ hx â†¦ he.1 hx) he.2.1 he.2.2 (fun _ hx â†¦ congrArg f (he' hx).symm)

end Finset

variable {Î± : Type*} [DecidableEq Î±]
variable {Î² : Type*} [DecidableEq Î²]

namespace Multiset

@[simp]
lemma multinomial_zero : multinomial (0 : Multiset Î±) = 1 := rfl

theorem card_filter_ne (a : Î±) (s : Multiset Î±) :
    card (s.filter (fun x => x â‰  a)) = card s - s.count a := by
  conv => rhs; lhs; rw [â† Multiset.filter_add_not (fun x => x = a) s]
  simp only [_root_.map_add, count_eq_card_filter_eq, ne_eq, eq_comm (a := a)]
  rw [Nat.add_sub_cancel_left]

end Multiset

namespace Prod

def mkFstEmbedding (b : Î²) : Î± â†ª Î± Ã— Î² := âŸ¨fun x â†¦ (x, b), by simp [Function.Injective]âŸ©
def mkSndEmbedding (a : Î±) : Î² â†ª Î± Ã— Î² := âŸ¨fun x â†¦ (a, x), by simp [Function.Injective]âŸ©

@[simp] lemma mkFstEmbedding_apply (b : Î²) (x : Î±) : mkFstEmbedding b x = (x, b) := rfl
@[simp] lemma mkSndEmbedding_apply (a : Î±) (x : Î²) : mkSndEmbedding a x = (a, x) := rfl

end Prod

-- lemma Multiset.multinomial_eq {m : Multiset Î±} : m.multinomial = Nat.multinomial m.toFinset m.count := by
--   simp [Multiset.multinomial, Finsupp.multinomial_eq]
--   rfl

variable {E : Type*}  -- TODO: Rename/remove?

-- `[CommMonoid A]` for `Finset.prod`
-- `[NormedRing A] [NormedAlgebra ğ•œ A]` to be like `norm_iteratedFDeriv_mul_le`
variable {A : Type*} [CommSemiring A]  -- CommMonoid?


section Split

namespace Finset

-- TODO: Would it be better to use `Fin k.succ Ã— Multiset Î±`?
/--
Image of count-cons-remove on `Finset.multichoose k s`, defined as a `Finset`.
Equivalent to `{q : â„• Ã— Multiset (Fin n) | q.1 â‰¤ k âˆ§ q.2 âˆˆ Finset.multichoose (k - q.1) Finset.univ}`.
-/
def multichooseSplit (k : â„•) (s : Finset Î±) (x : Î±) : Finset (â„• Ã— Multiset Î±) :=
  (range k.succ).biUnion fun m â†¦ (multichoose (k - m) (s.erase x)).map (Prod.mkSndEmbedding m)

-- TODO: Add proof of image.

theorem mem_multichooseSplit_iff {k : â„•} {s : Finset Î±} {x : Î±} {q : â„• Ã— Multiset Î±} :
    q âˆˆ multichooseSplit k s x â†” q.1 â‰¤ k âˆ§ q.2 âˆˆ multichoose (k - q.1) (s.erase x) := by
  rcases q with âŸ¨m, tâŸ©
  simp [multichooseSplit, Nat.lt_succ]

/-- Helper lemma for `multichooseSplitEquiv`. -/
theorem consCountFilterNe_mem_multichooseSplit {k : â„•} {s : Finset Î±} {x : Î±}
    {t : Multiset Î±} (ht : t âˆˆ multichoose k s) :
    (Multiset.count x t, Multiset.filter (fun a => a â‰  x) t) âˆˆ multichooseSplit k s x := by
  rw [mem_multichoose_iff] at ht
  rcases ht with âŸ¨ht_card, ht_memâŸ©
  simp only [â† ht_card]
  refine mem_multichooseSplit_iff.mpr (And.intro ?_ ?_)
  Â· exact Multiset.count_le_card x t
  Â· refine mem_multichoose_iff.mpr (And.intro ?_ ?_)
    Â· rw [â† Multiset.card_filter_ne]
    Â· simp only [Multiset.mem_filter, mem_erase, and_imp]
      intro a ha
      simp [ht_mem a ha]

/-- Helper lemma for `multichooseSplitEquiv`. -/
theorem replicateAdd_mem_multichoose {k : â„•} {s : Finset Î±} {x : Î±} (hx : x âˆˆ s)
    {q : â„• Ã— Multiset Î±} (hq : q âˆˆ multichooseSplit k s x) :
    Multiset.replicate q.1 x + q.2 âˆˆ multichoose k s := by
  rcases q with âŸ¨m, tâŸ©
  simp only [mem_multichooseSplit_iff, mem_multichoose_iff] at hq
  rcases hq with âŸ¨hm, ht_card, ht_memâŸ©
  refine mem_multichoose_iff.mpr (And.intro ?_ ?_)
  Â· simp [ht_card, hm]
  Â· intro a ha
    simp only [Multiset.mem_add, Multiset.mem_replicate] at ha
    cases ha with
    | inl ha => rw [ha.2]; exact hx
    | inr ha => exact mem_of_mem_erase (ht_mem a ha)


theorem mapsTo_consCountFilterNe_multichoose {k : â„•} {s : Finset Î±} {x : Î±} :
    Set.MapsTo (fun t â†¦ (Multiset.count x t, Multiset.filter (fun a => a â‰  x) t))
      (multichoose k s) (multichooseSplit k s x) :=
  fun _ ht â†¦ consCountFilterNe_mem_multichooseSplit ht

theorem mapsTo_replicateAdd_multichooseSplit {k : â„•} {s : Finset Î±} {x : Î±} (hx : x âˆˆ s) :
    Set.MapsTo (fun q : â„• Ã— Multiset Î± â†¦ Multiset.replicate q.1 x + q.2)
      (multichooseSplit k s x) (multichoose k s) :=
  fun _ hq â†¦ replicateAdd_mem_multichoose hx hq

theorem leftInvOn_consCountFilterNe_multichoose {k : â„•} {s : Finset Î±} {x : Î±} :
    Set.LeftInvOn (fun q â†¦ Multiset.replicate q.1 x + q.2)
      (fun t â†¦ (Multiset.count x t, Multiset.filter (fun a => a â‰  x) t))
      (multichoose k s) := by
  simp [Set.LeftInvOn, â† Multiset.filter_eq', Multiset.filter_add_not]

theorem leftInvOn_replicateAdd_multichooseSplit {k : â„•} {s : Finset Î±} {x : Î±} :
    Set.LeftInvOn (fun t â†¦ (Multiset.count x t, Multiset.filter (fun a => a â‰  x) t))
      (fun q â†¦ Multiset.replicate q.1 x + q.2)
      (multichooseSplit k s x) := by
  rw [Set.LeftInvOn]
  intro q
  rcases q with âŸ¨m, tâŸ©
  simp only [mem_coe, mem_multichooseSplit_iff, mem_multichoose_iff, and_imp]
  intro _ _ ht_mem
  refine Prod.mk.inj_iff.mpr (And.intro ?_ ?_)
  Â· rw [Multiset.count_add, Multiset.count_replicate_self]
    rw [add_right_eq_self, Multiset.count_eq_zero]
    intro hxt
    exact not_mem_erase x s (ht_mem x hxt)
  Â· rw [Multiset.filter_add]
    rw [Multiset.filter_eq_nil.mpr (by simp [Multiset.mem_replicate])]
    rw [Multiset.filter_eq_self.mpr (fun a ha â†¦ ne_of_mem_erase (ht_mem a ha))]
    simp

-- Combine `LeftInvOn` and `MapsTo` to obtain `injOn`, `surjOn`, `bijOn`.

theorem injOn_consCountFilterNe_multichoose {k : â„•} {s : Finset Î±} {x : Î±} :
    Set.InjOn (fun t â†¦ (Multiset.count x t, Multiset.filter (fun a => a â‰  x) t))
      (multichoose k s) :=
  leftInvOn_consCountFilterNe_multichoose.injOn

theorem injOn_replicateAdd_multichooseSplit {k : â„•} {s : Finset Î±} {x : Î±} :
    Set.InjOn (fun q : â„• Ã— Multiset Î± â†¦ Multiset.replicate q.1 x + q.2)
      (multichooseSplit k s x) :=
  leftInvOn_replicateAdd_multichooseSplit.injOn

theorem surjOn_consCountFilterNe_multichoose {k : â„•} {s : Finset Î±} {x : Î±} (hx : x âˆˆ s) :
    Set.SurjOn (fun t â†¦ (Multiset.count x t, Multiset.filter (fun a => a â‰  x) t))
      (multichoose k s) (multichooseSplit k s x) :=
  leftInvOn_replicateAdd_multichooseSplit.surjOn (mapsTo_replicateAdd_multichooseSplit hx)

theorem surjOn_replicateAdd_multichooseSplit {k : â„•} {s : Finset Î±} {x : Î±} :
    Set.SurjOn (fun q : â„• Ã— Multiset Î± â†¦ Multiset.replicate q.1 x + q.2)
      (multichooseSplit k s x) (multichoose k s) :=
  leftInvOn_consCountFilterNe_multichoose.surjOn (mapsTo_consCountFilterNe_multichoose)

theorem bijOn_consCountFilterNe_multichoose {k : â„•} {s : Finset Î±} {x : Î±} (hx : x âˆˆ s) :
    Set.BijOn (fun t â†¦ (Multiset.count x t, Multiset.filter (fun a => a â‰  x) t))
      (multichoose k s) (multichooseSplit k s x) :=
  âŸ¨mapsTo_consCountFilterNe_multichoose,
    âŸ¨injOn_consCountFilterNe_multichoose, surjOn_consCountFilterNe_multichoose hxâŸ©âŸ©

theorem bijOn_addReplicate_multichooseSplit {k : â„•} {s : Finset Î±} {x : Î±} (hx : x âˆˆ s) :
    Set.BijOn (fun q : â„• Ã— Multiset Î± â†¦ Multiset.replicate q.1 x + q.2)
      (multichooseSplit k s x) (multichoose k s) :=
  âŸ¨mapsTo_replicateAdd_multichooseSplit hx,
    âŸ¨injOn_replicateAdd_multichooseSplit, surjOn_replicateAdd_multichooseSplitâŸ©âŸ©

-- TODO: `multichoose_eq_biUnion_multichoose_erase`

-- def multichooseSplitEquiv (k : â„•) {s : Finset Î±} {x : Î±} (hx : x âˆˆ s) :
--     â†‘(multichoose k s) â‰ƒ â†‘(multichooseSplit k s x) where
--   toFun
--     | âŸ¨t, htâŸ© => âŸ¨âŸ¨t.count x, t.filter (fun a â†¦ a â‰  x)âŸ©, consCountFilterNe_mem_multichooseSplit htâŸ©
--   invFun
--     | âŸ¨q, hqâŸ© => âŸ¨Multiset.replicate q.1 x + q.2, replicateAdd_mem_multichoose hx hqâŸ©
--   left_inv := by
--     simp [Function.LeftInverse, â† Multiset.filter_eq', Multiset.filter_add_not]
--   right_inv := by
--     simp only [Function.RightInverse, Function.LeftInverse, Subtype.forall, Subtype.mk.injEq]
--     intro q
--     rcases q with âŸ¨m, tâŸ©
--     simp only [mem_multichooseSplit_iff, mem_multichoose_iff, and_imp]
--     intro _ _ ht_mem
--     refine Prod.mk.inj_iff.mpr (And.intro ?_ ?_)
--     Â· rw [Multiset.count_add, Multiset.count_replicate_self]
--       rw [add_right_eq_self, Multiset.count_eq_zero]
--       intro hxt
--       exact not_mem_erase x s (ht_mem x hxt)
--     Â· rw [Multiset.filter_add]
--       rw [Multiset.filter_eq_nil.mpr (by simp [Multiset.mem_replicate])]
--       rw [Multiset.filter_eq_self.mpr (fun a ha â†¦ ne_of_mem_erase (ht_mem a ha))]
--       simp

-- @[simp]
-- lemma apply_multichooseSplitEquiv (k : â„•) {s : Finset Î±} {x : Î±} (hx : x âˆˆ s)
--     (t : multichoose k s) :
--     (multichooseSplitEquiv k hx t).val = (t.val.count x, t.val.filter (fun a â†¦ a â‰  x)) := rfl

-- @[simp]
-- lemma apply_multichooseSplitEquiv_symm (k : â„•) {s : Finset Î±} {x : Î±} (hx : x âˆˆ s)
--     (q : multichooseSplit k s x) :
--     ((multichooseSplitEquiv k hx).symm q).val = Multiset.replicate q.val.fst x + q.val.snd := rfl

lemma pairwiseDisjoint_multichooseSplit (k : â„•) (s : Finset Î±) (x : Î±) :
    Set.PairwiseDisjoint â†‘(range (Nat.succ k))
      fun m â†¦ ((s.erase x).multichoose (k - m)).map (Prod.mkSndEmbedding m) := by
  intro i _ j _ hij
  simp [disjoint_iff_ne, hij]

end Finset  -- namespace
end Split

namespace Finset

theorem pow_sum {p : â„•} {s : Finset Î±} {f : Î± â†’ A} :
    (âˆ‘ i in s, f i) ^ p = âˆ‘ k in s.multichoose p, k.multinomial * âˆ i in s, f i ^ k.count i := by
  induction s using Finset.induction generalizing p with
  | empty => cases p <;> simp
  | @insert a s ha ih =>
    -- Apply binomial theorem on left.
    rw [Finset.sum_insert ha, add_pow]
    -- Re-index sum on right.
    rw [sum_bijOn_comp_leftInv (bijOn_consCountFilterNe_multichoose (mem_insert_self a s))
      leftInvOn_consCountFilterNe_multichoose]
    rw [multichooseSplit]
    rw [sum_biUnion (pairwiseDisjoint_multichooseSplit p _ a)]
    refine Finset.sum_congr rfl ?_
    simp only [mem_range, Nat.lt_succ]
    intro m hmp
    -- Apply inductive hypothesis on left.
    simp only [ih, Finset.mul_sum, Finset.sum_mul]
    -- Simplify inner sum on right.
    simp only [sum_map, erase_insert ha, Prod.mkSndEmbedding_apply]
    refine Finset.sum_congr rfl ?_
    intro t ht
    -- Separate the multinomial and product terms.
    suffices : (Multiset.replicate m a + t).multinomial = p.choose m * t.multinomial âˆ§
        âˆ i in insert a s, f i ^ (Multiset.replicate m a + t).count i = f a ^ m * âˆ i in s, f i ^ t.count i
    Â· rw [this.1, this.2, Nat.cast_mul]
      ring_nf
    rw [Finset.mem_multichoose_iff] at ht
    -- `s` is a disjoint union of `t` and `{a}`.
    have hat : a âˆ‰ t := fun h â†¦ ha (ht.2 a h)
    have hs_ne : âˆ€ i âˆˆ s, i â‰  a := fun i his hia â†¦ by rw [hia] at his; exact ha his
    refine And.intro ?_ ?_
    Â· -- Split the multinomial term.
      rw [Multiset.multinomial_filter_ne a]
      refine congrArgâ‚‚ _ ?_ ?_
      Â· simp [ht.1, hmp, hat]
      Â· rw [Multiset.filter_add]
        simp only [ne_comm (a := a), Multiset.filter_add]
        rw [Multiset.filter_eq_nil.mpr (by simp [Multiset.mem_replicate])]
        rw [Multiset.filter_eq_self.mpr (fun u hu â†¦ ne_of_mem_of_not_mem hu hat)]
        rw [zero_add]
    Â· -- Split the product.
      rw [Finset.prod_insert ha]
      refine congrArgâ‚‚ _ ?_ (Finset.prod_congr rfl ?_)
      Â· simp [hat]
      Â· intro i hi
        simp [Multiset.count_replicate, hs_ne i hi]

end Finset
