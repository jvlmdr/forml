import Mathlib.MeasureTheory.Function.L1Space
import Mathlib.MeasureTheory.Function.LpSpace
import Mathlib.MeasureTheory.Integral.Bochner
import Mathlib.MeasureTheory.Integral.MeanInequalities

open MeasureTheory

open scoped Real NNReal ENNReal

namespace ENNReal

-- def IsConjugateExponent (p q : â„â‰¥0âˆ) : Prop := pâ»Â¹ + qâ»Â¹ = 1

-- Use `(1 - pâ»Â¹)â»Â¹` rather than `p / (p - 1)` to avoid `âˆ / (âˆ - 1)`.
noncomputable def conjugateExponent (p : â„â‰¥0âˆ) : â„â‰¥0âˆ := (1 - pâ»Â¹)â»Â¹

variable {p q : â„â‰¥0âˆ}

lemma one_le_conjugateExponent : 1 â‰¤ conjugateExponent p := by
  simp [conjugateExponent]

instance one_le_conjugateExponent' : Fact (1 â‰¤ conjugateExponent p) :=
  âŸ¨one_le_conjugateExponentâŸ©

@[simp]
lemma sub_right_eq_self {a b : â„â‰¥0âˆ} (ha : a â‰  0) (ha' : a â‰  âŠ¤) : a - b = a â†” b = 0 := by
  refine Iff.intro ?_ ?_
  . intro h
    suffices hba : b â‰¤ a
    . simpa [â† ENNReal.sub_right_inj ha' hba]
    have hba : 0 < a - b
    . simpa [h, zero_lt_iff]
    simp at hba
    exact hba.le
  . intro h
    simp [h]

-- Uses `sub_right_eq_self` with `a = 1`.
lemma conjugateExponent_eq_one : conjugateExponent p = 1 â†” p = âŠ¤ := by
  simp [conjugateExponent, inv_eq_iff_eq_inv]

lemma conjugateExponent_eq_top (hp : 1 â‰¤ p) : conjugateExponent p = âŠ¤ â†” p = 1 := by
  simp [conjugateExponent]
  exact LE.le.le_iff_eq hp

lemma one_le_of_conjugate (hpq : pâ»Â¹ + qâ»Â¹ = 1) : 1 â‰¤ p := by
  rw [â† ENNReal.inv_le_inv, inv_one]
  simp [â† hpq]

lemma one_le_of_conjugate' (hpq : pâ»Â¹ + qâ»Â¹ = 1) : 1 â‰¤ q := by
  rw [add_comm] at hpq
  exact one_le_of_conjugate hpq

/-- Like `Real.IsConjugateExponent.toReal` for `â„â‰¥0âˆ`.

Note that it is not necessary to include `1 â‰¤ p` in the definition `pâ»Â¹ + qâ»Â¹ = 1`.
-/
lemma conjugate_iff : (1 â‰¤ p âˆ§ q = conjugateExponent p) â†” pâ»Â¹ + qâ»Â¹ = 1 := by
  rw [conjugateExponent]
  rw [â† inv_eq_iff_eq_inv]
  refine Iff.intro ?_ ?_
  . simp
    intro hp h
    rw [h]
    rw [add_tsub_cancel_iff_le]
    simp
    exact hp
  . intro h
    have hp : 1 â‰¤ p := one_le_of_conjugate h
    refine And.intro hp ?_
    rw [â† h]
    rw [ENNReal.add_sub_cancel_left]
    rw [â† lt_top_iff_ne_top]
    simp
    exact lt_of_lt_of_le zero_lt_one hp

lemma conjugate_conjugateExponent (hp : 1 â‰¤ p) : pâ»Â¹ + (conjugateExponent p)â»Â¹ = 1 := by
  simp [conjugateExponent]
  simp [add_tsub_cancel_iff_le]
  exact hp

/-- Lighter version of `conjugate_cases`. -/
lemma conjugate_cases' (h : pâ»Â¹ + qâ»Â¹ = 1) :
    (p â‰  âŠ¤ âˆ§ q â‰  âŠ¤) âˆ¨ (p = 1 âˆ§ q = âŠ¤) âˆ¨ (p = âŠ¤ âˆ§ q = 1) := by
  cases p with
  | none =>
    simpa [inv_eq_iff_eq_inv] using h
  | some p =>
    cases q with
    | none =>
      simpa [inv_eq_iff_eq_inv] using h
    | some q =>
      simp

/-- Possible pairs are `(1, âŠ¤)`, `(âŠ¤, 1)`, or `(p, q)` with `1 < p, q < âŠ¤`. -/
lemma conjugate_cases (h : pâ»Â¹ + qâ»Â¹ = 1) :
    ((1 < p âˆ§ p â‰  âŠ¤) âˆ§ (1 < q âˆ§ q â‰  âŠ¤)) âˆ¨ (p = 1 âˆ§ q = âŠ¤) âˆ¨ (p = âŠ¤ âˆ§ q = 1) := by
  cases eq_or_lt_of_le (le_top : p â‰¤ âŠ¤) with
  | inl hp =>
    simp [hp] at h âŠ¢
    simpa [inv_eq_iff_eq_inv] using h
  | inr hp =>
    cases eq_or_lt_of_le (le_top : q â‰¤ âŠ¤) with
    | inl hq =>
      simp [hq] at h âŠ¢
      simpa [inv_eq_iff_eq_inv] using h
    | inr hq =>
      refine Or.inl ?_
      simp [â† lt_top_iff_ne_top, hp, hq]
      simp_rw [â† ENNReal.inv_lt_one]
      rw [â† h]
      rw [lt_top_iff_ne_top] at hp hq
      refine And.intro ?_ ?_
      . refine ENNReal.lt_add_right ?_ ?_
        . rw [â† lt_top_iff_ne_top]
          simp
          exact lt_of_lt_of_le zero_lt_one (one_le_of_conjugate h)
        . rwa [ENNReal.inv_ne_zero]
      . rw [add_comm]
        refine ENNReal.lt_add_right ?_ ?_
        . rw [â† lt_top_iff_ne_top]
          simp
          exact lt_of_lt_of_le zero_lt_one (one_le_of_conjugate' h)
        . rwa [ENNReal.inv_ne_zero]

/-- Obtain `Real.IsConjugateExponent` when both `p` and `q` are finite. -/
lemma isConjugateExponent_toReal (hpq : pâ»Â¹ + qâ»Â¹ = 1) (hp : p â‰  âŠ¤) (hq : q â‰  âŠ¤) :
    Real.IsConjugateExponent (p.toNNReal) (q.toNNReal) := by
  cases conjugate_cases hpq with
  | inr hpq =>
    simp at *
    cases hpq with
    | inl hpq => rcases hpq with âŸ¨_, hq'âŸ©; contradiction
    | inr hpq => rcases hpq with âŸ¨hp', _âŸ©; contradiction
  | inl hpq =>
    rcases hpq with âŸ¨hp, hqâŸ©
    have hp' : p â‰  0
    . refine ne_of_gt (lt_of_lt_of_le zero_lt_one ?_)
      exact one_le_of_conjugate hpq
    have hq' : q â‰  0
    . refine ne_of_gt (lt_of_lt_of_le zero_lt_one ?_)
      exact one_le_of_conjugate' hpq
    -- Move to NNReal for coercion to â„.
    cases p with | none => contradiction | some p =>
    cases q with | none => contradiction | some q =>
    simp at *
    refine Real.IsConjugateExponent.mk ?_ ?_
    . norm_cast
    . rw [â† ENNReal.coe_inv hp', â† ENNReal.coe_inv hq'] at hpq
      norm_cast at hpq
      simp
      norm_cast

end ENNReal


variable {E : Type*} [MeasurableSpace E]
variable {Î¼ : Measure E}
variable {p q : â„â‰¥0âˆ} (hpq : pâ»Â¹ + qâ»Â¹ = 1)

-- TODO: Think of a namespace? Follow e.g. `NNReal.lintegral_mul_le_Lp_mul_Lq`?
section Mul

variable {ğ•œ : Type*} [NormedRing ğ•œ]
-- Use `NormedRing` because we need `NormedAddCommGroup` for `Memâ„’p` and
-- `NonUnitalSeminormedRing` for `norm_mul_le`.

section Measurable
variable {f : E â†’ ğ•œ} (hf : AEStronglyMeasurable f Î¼)
variable {g : E â†’ ğ•œ} (hg : AEStronglyMeasurable g Î¼)

/-- **HÃ¶lder's inequality** for functions.

Compared to `NNReal.lintegral_mul_le_Lp_mul_Lq`, this theorem supports
`p, q âˆˆ [1, âˆ]` inclusive, and `NormedRing` rather than `NNReal`.
-/
theorem snorm_mul_L1_le_snorm_Lp_mul_snorm_Lq :
    snorm (f * g) 1 Î¼ â‰¤ snorm f p Î¼ * snorm g q Î¼ := by
  cases ENNReal.conjugate_cases hpq with
  | inl hpq =>
    rcases hpq with âŸ¨hp, hqâŸ©
    have hp' : p â‰  0
    . refine ne_of_gt (lt_of_lt_of_le zero_lt_one ?_)
      exact ENNReal.one_le_of_conjugate hpq
    have hq' : q â‰  0
    . refine ne_of_gt (lt_of_lt_of_le zero_lt_one ?_)
      exact ENNReal.one_le_of_conjugate' hpq
    rw [snorm_eq_lintegral_rpow_nnnorm hp' hp.right]
    rw [snorm_eq_lintegral_rpow_nnnorm hq' hq.right]
    rw [snorm_one_eq_lintegral_nnnorm]
    simp [-one_div]
    refine le_trans ?_ (NNReal.lintegral_mul_le_Lp_mul_Lq ?_ ?_ ?_)
    rotate_left
    . exact ENNReal.isConjugateExponent_toReal hpq hp.right hq.right
    . exact hf.nnnorm.aemeasurable
    . exact hg.nnnorm.aemeasurable
    simp
    refine lintegral_mono ?_
    intro x
    simp
    norm_cast
    exact nnnorm_mul_le (f x) (g x)

  | inr hpq =>
    cases hpq with
    | inl hpq =>
      simp [hpq.left, hpq.right, -snorm_exponent_top] at *
      refine snorm_le_snorm_mul_snorm_top 1 hf g (fun u v => u * v) ?_
      refine Filter.eventually_of_forall ?_
      intro x
      simp
      exact nnnorm_mul_le (f x) (g x)
    | inr hpq =>
      simp [hpq.left, hpq.right, -snorm_exponent_top] at *
      refine snorm_le_snorm_top_mul_snorm 1 f hg (fun u v => u * v) ?_
      refine Filter.eventually_of_forall ?_
      intro x
      simp
      exact nnnorm_mul_le (f x) (g x)

end Measurable

end Mul


-- TODO: Think of a namespace? Follow e.g. `NNReal.lintegral_mul_le_Lp_mul_Lq`?
section SMul

variable {ğ•œ : Type*} [NormedAddCommGroup ğ•œ]
variable {F : Type*} [NormedAddCommGroup F]
-- TODO: Replace these with something more general that includes above?
-- `NormedSpace ğ•œ F` with `NormedField ğ•œ` could work, but it seems less general
-- as it requires that `â€–c â€¢ xâ€– â‰¤ â€–câ€– * â€–xâ€–` holds with equality.
variable [SMulZeroClass ğ•œ F] [BoundedSMul ğ•œ F]

section Measurable
variable {f : E â†’ ğ•œ} (hf : AEStronglyMeasurable f Î¼)
variable {g : E â†’ F} (hg : AEStronglyMeasurable g Î¼)

/-- **HÃ¶lder's inequality** for `f â€¢ g`. -/
theorem snorm_smul_L1_le_snorm_Lp_mul_snorm_Lq :
    snorm (f â€¢ g) 1 Î¼ â‰¤ snorm f p Î¼ * snorm g q Î¼ := by
  -- Convert to multiplication of norms and use `snorm_mul_L1_le_snorm_Lp_mul_snorm_Lq`.
  suffices : snorm (fun x => â€–f x â€¢ g xâ€–) 1 Î¼ â‰¤ snorm (fun x => â€–f xâ€– * â€–g xâ€–) 1 Î¼
  . rw [â† snorm_norm (f â€¢ g), â† snorm_norm f, â† snorm_norm g]
    refine le_trans this ?_
    exact snorm_mul_L1_le_snorm_Lp_mul_snorm_Lq hpq hf.norm hg.norm
  simp
  refine snorm_mono ?_
  simp
  intro x
  exact norm_smul_le (f x) (g x)

end Measurable

section Memâ„’p
variable {f : E â†’ ğ•œ} (hf : Memâ„’p f p Î¼)
variable {g : E â†’ F} (hg : Memâ„’p g q Î¼)

/-- Uses HÃ¶lder's inequality to show that `f â€¢ g` is in `L1`. -/
theorem memL1_Lp_smul_Lq  : Memâ„’p (f â€¢ g) 1 Î¼ := by
  refine And.intro ?_ ?_
  . exact AEStronglyMeasurable.smul hf.aestronglyMeasurable hg.aestronglyMeasurable
  . refine lt_of_le_of_lt (snorm_smul_L1_le_snorm_Lp_mul_snorm_Lq hpq ?_ ?_) ?_
    . exact hf.aestronglyMeasurable
    . exact hg.aestronglyMeasurable
    exact ENNReal.mul_lt_top hf.snorm_ne_top hg.snorm_ne_top

lemma integrable_Lp_smul_Lq  : Integrable (f â€¢ g) Î¼ := by
  rw [â† memâ„’p_one_iff_integrable]
  exact memL1_Lp_smul_Lq hpq hf hg

end Memâ„’p

section Lp
variable {f : Lp ğ•œ p Î¼}
variable {g : Lp F q Î¼}

section Def
variable (f g)

/-- Constructs an element of `L1` from `f â€¢ g` using HÃ¶lder's inequality for functions. -/
noncomputable def L1_of_Lp_smul_Lq : Lp F 1 Î¼ :=
  Memâ„’p.toLp ((f : E â†’ ğ•œ) â€¢ (g : E â†’ F)) (memL1_Lp_smul_Lq hpq (Lp.memâ„’p f) (Lp.memâ„’p g))

end Def

lemma coeFn_L1_of_Lp_smul_Lq :
    L1_of_Lp_smul_Lq hpq f g =áµ[Î¼] (f : E â†’ ğ•œ) â€¢ (g : E â†’ F) := by
  simp [L1_of_Lp_smul_Lq, Memâ„’p.coeFn_toLp]

/-- **HÃ¶lder's inequality** for `f â€¢ g`, expressed using `Lp` norms. -/
theorem norm_L1_of_smul_le_norm_Lp_mul_norm_Lq :
    â€–L1_of_Lp_smul_Lq hpq f gâ€– â‰¤ â€–fâ€– * â€–gâ€– := by
  -- Combine finiteness and boundedness.
  generalize hÎ¾ : L1_of_Lp_smul_Lq hpq f g = Î¾
  simp [Lp.norm_def]
  rw [â† ENNReal.toReal_mul]
  rw [ENNReal.toReal_le_toReal]
  rotate_left
  . exact Lp.snorm_ne_top _
  . exact ENNReal.mul_ne_top (Lp.snorm_ne_top _) (Lp.snorm_ne_top _)
  -- Need to propagate through the `AEEqFun` of `Memâ„’p.toLp`; use `snorm_congr_ae`.
  rw [â† hÎ¾]
  rw [snorm_congr_ae (coeFn_L1_of_Lp_smul_Lq hpq)]
  refine snorm_smul_L1_le_snorm_Lp_mul_snorm_Lq hpq ?_ ?_
  . exact (Lp.memâ„’p f).aestronglyMeasurable
  . exact (Lp.memâ„’p g).aestronglyMeasurable

end Lp

end SMul


-- TODO: Could reduce duplication by defining a general version of the above.
-- However, it might be easier to just copy it for now.
section Mul

variable {ğ•œ : Type*} [NormedRing ğ•œ]

section Memâ„’p
variable {f : E â†’ ğ•œ} (hf : Memâ„’p f p Î¼)
variable {g : E â†’ ğ•œ} (hg : Memâ„’p g q Î¼)

/-- Uses HÃ¶lder's inequality for functions to show that `f * g` is in `L1`. -/
theorem memL1_Lp_mul_Lq  : Memâ„’p (f * g) 1 Î¼ := by
  refine And.intro ?_ ?_
  . exact AEStronglyMeasurable.mul hf.aestronglyMeasurable hg.aestronglyMeasurable
  . refine lt_of_le_of_lt (snorm_mul_L1_le_snorm_Lp_mul_snorm_Lq hpq ?_ ?_) ?_
    . exact hf.aestronglyMeasurable
    . exact hg.aestronglyMeasurable
    exact ENNReal.mul_lt_top hf.snorm_ne_top hg.snorm_ne_top

lemma integrable_Lp_mul_Lq  : Integrable (f * g) Î¼ := by
  rw [â† memâ„’p_one_iff_integrable]
  exact memL1_Lp_mul_Lq hpq hf hg

end Memâ„’p

section Lp
variable {f : Lp ğ•œ p Î¼}
variable {g : Lp ğ•œ q Î¼}

section Def
variable (f g)

/-- Constructs an element of `L1` from `f * g` using HÃ¶lder's inequality for functions. -/
noncomputable def L1_of_mul : Lp ğ•œ 1 Î¼ :=
  Memâ„’p.toLp (f * g) (memL1_Lp_mul_Lq hpq (Lp.memâ„’p f) (Lp.memâ„’p g))

end Def

lemma coeFn_L1_of_mul :
    L1_of_mul hpq f g =áµ[Î¼] f * g := by
  simp [L1_of_mul, Memâ„’p.coeFn_toLp]

/-- **HÃ¶lder's inequality** for `f * g`, expressed using `Lp` norms. -/
theorem norm_L1_of_mul_le_norm_Lp_mul_norm_Lq :
    â€–L1_of_mul hpq f gâ€– â‰¤ â€–fâ€– * â€–gâ€– := by
  -- Combine finiteness and boundedness.
  generalize hÎ¾ : L1_of_mul hpq f g = Î¾
  simp [Lp.norm_def]
  rw [â† ENNReal.toReal_mul]
  rw [ENNReal.toReal_le_toReal]
  rotate_left
  . exact Lp.snorm_ne_top _
  . exact ENNReal.mul_ne_top (Lp.snorm_ne_top _) (Lp.snorm_ne_top _)
  -- Need to propagate through the `AEEqFun` of `Memâ„’p.toLp`; use `snorm_congr_ae`.
  rw [â† hÎ¾]
  rw [snorm_congr_ae (coeFn_L1_of_mul hpq)]
  refine snorm_mul_L1_le_snorm_Lp_mul_snorm_Lq hpq ?_ ?_
  . exact (Lp.memâ„’p f).aestronglyMeasurable
  . exact (Lp.memâ„’p g).aestronglyMeasurable

end Lp

end Mul
